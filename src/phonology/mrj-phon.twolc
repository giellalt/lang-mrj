!! !!!Hill Mari twol file

!! !!Alphabet, Sets and Definitions

Alphabet

!! !!Letters of the alphabet

а б в г д е ё ж з и й к л м н ҥ о ӧ п р с т у ӱ ф х ц ч ш щ ъ ы ь э ю я ӓ ӹ !!= * @CODE@
А Б В Г Д Е Ё Ж З И Й К Л М Н Ҥ О Ӧ П Р С Т У Ӱ Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӓ Ӹ !!= * @CODE@

!! !!Archiphonemes for vowels
!  е1:е а1:а и1:и у1:у ӱ1:ӱ
!  Е1:Е %{аӓ%}:А И1:И У1:У Ӱ1:Ӱ
! JMR 2012-09-18
! %{аӓ%}:а 
 %{аӓ%}:ӓ  !!= * @CODE@ a/ä alternation in vowel harmony

 Е3:е !!= * @CODE@ ӓвӓмвлӓ 
! Е2:е != * @CODE@ 
 Е2:0 !!= * @CODE@
 %{ыӹ%0%}:0 !!= * @CODE@ 

! Ы2:ы
! Ы2:ӹ
 Ы2:0  !!= * @CODE@ 
 А2:а !!= * @CODE@ 
! А2:я

 Ь2:0 !!= * @CODE@  except with V АМ +Prt1+Sg1-3

!! !!Triggers

%^ConsTransf:0 !!= * @CODE@ 
%^KIN:0        !!= * @CODE@  with ӓвӓ in +PxSg1+Sg+Gen etc.
%^А2ЫӸ:0       !!= * @CODE@  with карта and картын витринӹн

!! !!Boundary symbols

 %> !!= * @CODE@ 
 #  !!  * hash #
 %- !!= * @CODE@ 


 ;


!! !!Sets 

Sets
BackLabVow =   о у ё ю О У Ё Ю ;
FrontLabVow =   ӧ ӱ Ӧ Ӱ;
NonLabVow =   ӓ а е и э я Ӓ А Е И Э Я ;
Vow =  BackLabVow FrontLabVow NonLabVow ы Ы  ӹ Ӹ ; 
BackVow = BackLabVow а А ы Ы ;
FrontVow = FrontLabVow е и э ӓ ӹ Е И Э Ӓ Ӹ ;
ArchiVow = %{ыӹ%0%} Ы2 %{аӓ%} ;

CnsNoLN =  б в г д ж з к м ҥ п р с т ф х ц ч ш 
           Б В Г Д Ж З К М Ҥ П Р С Т Ф Х Ц Ч Ш ;


Cns =  б в г д ж з к л м н ҥ п р с т ф х ц ч ш 
       Б В Г Д Ж З К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш ;
Cst =  к п т ф х ц ч ш с 
       К П Т Ф Х Ц Ч Ш Ц ;

Ltrs = Vow Cns Й й  ъ ь Ъ Ь ;

all =  Ltrs  %- ;



!! !!Definitions

Definitions

LFC = # Cns*  [ :FrontVow | Е2: | :е | :ӹ | :Ӹ ] \[ #: | :BackVow | :ы | :Ы ]* (%{аӓ%}:я ) \[ #: | :BackVow | :ы | :Ы ]*  (%^А2ЫӸ:0) ;

LBC = # Cns* [ :BackVow ] \[ #: | :FrontVow | :Ӹ | :ӹ ]* (%{аӓ%}:я) \[ #: | :FrontVow | :Ӹ | :ӹ ]* (%^А2ЫӸ:0) ;
! [ :BackVow ] ( %> | %^ConsTransf:0 | %^KIN:0 | BackVow | %{аӓ%}:а | %{аӓ%}:я | %{ыӹ%0%}:ы | Ь2:0 | %{ыӹ%0%}:0 | Е3:0 | Ы2:ы | Cns: )* ;


!! !!Rules

Rules

!! !!Palatalisation

"Palatal mark loss before vowel"
!! __@RULENAME@__  
    ь:0  <=>   _ %> А2: ;
   ! JMR 2012-09-18
      
"Ь2:ь Palatal mark for V АМ +Prt1+Sg1"
!! __@RULENAME@__  
Ь2:ь <=> [ н | л ] _ %> %{ыӹ%0%}: ;

"Ь2:0 Palatal mark for V АМ +Prt1+Sg1"
!! __@RULENAME@__  
Ь2:0 <=> [Vow | й | CnsNoLN: ] _ %> %{ыӹ%0%}: ;



!! !!Vowel rules
! JMR 2012-09-18
"Onset vowel loss in suffix ыӹ0"
!! __@RULENAME@__  
   %{ыӹ%0%}:0 <=> :Vow ( %^KIN:0 ) %> _ ;
           %> ж Ы2: %> _  ;
          [ а:ӹ | а:ы ] %^А2ЫӸ:0 %> _ ;

"Onset vowel loss in suffix Е3"
!! __@RULENAME@__  
   Е3:0 <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] %^KIN:0 %> _ ;
           %> ж Ы2: %> _  ;


! "Onset vowel loss in suffix Е2"
!! __@RULENAME@__  
!   Е2:0 <=> %> ж Ы2: %> _  Cns ;


"Onset vowel Е2 realized in suffix :е"
!! __@RULENAME@__
  Е2:е <=> [ Cns (Ы2: |ӹ:|ы:)| й: ] %> _ [в|м|н|т|ш] ;

"Onset vowel Е2:э after retained vowel"
!! __@RULENAME@__  
Е2:э <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] %> _ [в|м|н|т|ш] ;

"Onset vowel Е3:э after retained vowel"
!! __@RULENAME@__  
Е3:э <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] %> _ ;

"Stem final ы loss before Е2"
!! __@RULENAME@__  
ы:0 <=> #: Cns* Vow Cns+ _ %>  Е2: [в|м|н|т|ш] ;
           _ %> [ %{аӓ%}: [м|т|ш|%>|#] | Е2: [в|м|н|т|ш] | Е3: ] ;

"Stem final ӹ loss before Е2"
!! __@RULENAME@__  
ӹ:0 <=> #: Cns* Vow Cns+ _ %> Е2: [в|м|н|т|ш] ;
           _ %> [ %{аӓ%}: [м|т|ш|%>|#]| Е2: [в|м|н|т|ш] | Е3: ] ;


!! !!Vowel harmony

"Onset vowel ыӹ0 realized in suffix  %{ыӹ%0%}:ы"
!! __@RULENAME@__  
  %{ыӹ%0%}:ы <=> [ LBC  Cns  ((Ь2:) %> )| LBC ( н | л ) Ь2: %> | BackVow й: ( Ь2:0 ) %> ] _ ;
! [[ Cns | й ]+ %> | %> [ %> | BackVow | е | ArchiVow: | Cns ]* [ Cns | й ] ( %> ) ] _ ;

"Onset vowel %{ыӹ%0%} realized in suffix  %{ыӹ%0%}:ӹ"
!! __@RULENAME@__
  %{ыӹ%0%}:ӹ <=> LFC [ Cns  | й ] (Ь2:) %> _ ;

"Affix mid or final front %{аӓ%}:ӓ"
 %{аӓ%}:ӓ <=> [ LFC [ :Cns (%>)| %> Cns:+ ] | %> Е2:е [в|м|н|т|ш] Cns:* ] _ [м|н|т|ш|%>|#] ;

"Affix mid or final back %{аӓ%}:а"
!! __@RULENAME@__  
 %{аӓ%}:а <=> LBC [ й %> %{ыӹ%0%}: м  | :Cns  %> |р|н|д|л] _ [м|н|т|ш|к|%>|#];
          BackVow  й: %^ConsTransf:0 %> [ н | д ] _ [м|т|%>|#];
!€ колт>%{ыӹ%0%}д%{аӓ%}
!€ колт0ыда

"Affix mid or final back %{аӓ%}:я"
!! __@RULENAME@__  
 %{аӓ%}:я <=> Vow й:0 %> _ [м|т|ш|%>|#] ;

"Affix initial back а:я"
!! __@RULENAME@__  
 а:я <=> й:0 %> _ \[ %^А2ЫӸ: ] ;

"Not SgNom а:ы"
!! __@RULENAME@__
а:ы <=> BackVow Cns+ _ %^А2ЫӸ:0 %> ;

"Not SgNom а:ӹ"
!! __@RULENAME@__
а:ӹ <=> FrontVow Cns+ _ %^А2ЫӸ:0 %> ;


"suffix-final vowel backed Ы2ы"
!! __@RULENAME@__  
 Ы2:ы <=> LBC Cns _ [ м Ы2: | %>  (%{ыӹ%0%}:) Cns: | # ] ;
          LBC (й ( %^ConsTransf:0 )) %> [ ж | ш т ] _ [ %> (%{ыӹ%0%}:) Cns:  | # ] ;
!  LBC %> [ ж: | д Ы2: м | ( %{ыӹ%0%}: ) м | ( %{ыӹ%0%}: ) ш ] _ [ #: | %> [ л | г | ш [ т | к ] | д | %{ыӹ%0%}: [ м | н ] ]] ;
!          LBC %> д _ м Ы2: ;
! Work 2012-10-29            %>  %{ыӹ%0%}: ш [ т | к ]  _ %> %{ыӹ%0%}: ;



"suffix-final vowel fronted Ы2:ӹ"
!! __@RULENAME@__  
 Ы2:ӹ <=> LFC Cns _ [ м Ы2: | %> (%{ыӹ%0%}:) Cns: | # ] ;
          LFC (й ( %^ConsTransf:0 )) %> [ ж | ш т ] _ [ %> (%{ыӹ%0%}:) Cns:  | # ] ;
! LFC Cns _ [ %> :0* Cns: | # ] ;
!          LFC й ( %^ConsTransf:0 ) %> [ ж | ш т ] _ [ %>  :0* Cns:  | # ] ;

!! !!Consonant loss or transformation

"т:0 in am type verbs" 
!! __@RULENAME@__ лӓктӓш:лӓкнӓ
т:0 <=> к _ %^ConsTransf:0 ;

"к:0 in am type verbs" 
!! __@RULENAME@__ качкаш:качна
к:0 <=> [ ш | ч ] _ %^ConsTransf:0 ; 

"з:ц in am type verbs" 
!! __@RULENAME@__ вазаш:вацна
з:ц <=> [ Vow | н: ] _ %^ConsTransf:0 ;

"н:0 before з:ц in am type verbs" 
!! __@RULENAME@__ негӹнзӓш:негӹц
н:0 <=>  _ з: %^ConsTransf:0 ;

!! !!Vowel again

"suffix-final vowel loss before subsequent suffix-initial vowel Ы2:0" 
!! __@RULENAME@__  
  Ы2:0 <=>    Cns %> [ %{ыӹ%0%}: [ д Ы2: м | м | ш ( т | к ) ]| ж: ] _ %> [ Е2: [в|м|н|т|ш] | А2: ] ;
! Vow %> [ %{ыӹ%0%}: ш [ т | к ] | ж: ] _ %> [ л | г | %{ыӹ%0%}: ш | ш | д | Е2: [в|м|н|т|ш] | А2: ] ;


"й Deletion in front of я Suffix and others"
!! __@RULENAME@__  
  й:0 <=> _ ( %^ConsTransf:0 ) %> [ :я | :е | :и | :ю ]  ;



!! !!Devoicing
"Onset consonant devoicing" 
!! __@RULENAME@__  
 ж:ш <=> [ :Cst ( :0 ) | ж ] ( %^ConsTransf:0 ) %> _ [ %{ыӹ%0%}: | Ы2: ] ;
