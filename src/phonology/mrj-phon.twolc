!! !!!Hill Mari twol file

!! !!Alphabet, Sets and Definitions

Alphabet

!! !!Letters of the alphabet

а б в г д е ё ж з и й к л м н ҥ о ӧ п р с т у ӱ ф х ц ч ш щ ъ ы ь э ю я ӓ ӹ !!= * @CODE@
А Б В Г Д Е Ё Ж З И Й К Л М Н Ҥ О Ӧ П Р С Т У Ӱ Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӓ Ӹ !!= * @CODE@

!! !!Archiphonemes for vowels
!  е1:е а1:а и1:и у1:у ӱ1:ӱ
!  Е1:Е %{аӓ%}:А И1:И У1:У Ӱ1:Ӱ
! JMR 2012-09-18
! %{аӓ%}:а 
 %{аӓ%}:ӓ  !!= * @CODE@ a/ä alternation in vowel harmony

 Е3:е !!= * @CODE@ ӓвӓмвлӓ 
! Е2:е != * @CODE@ 
 Е2:0 !!= * @CODE@
 %{ыӹØ%}:0 !!= * @CODE@ 

 %{ыӹе%}:0 !! * @CODE@ used in verb affix onset
 %{ыӹэ%}:0 !! * @CODE@ used in noun affix onset ine, ill
 %{аӓе%}:0 !! * @CODE@ used in verb affix onset
! Ы2:ы
! Ы2:ӹ
 Ы2:0  !!= * @CODE@ 
 А2:а !!= * @CODE@ 
! А2:я

 Ь2:0 !!= * @CODE@  except with V АМ +Prt1+Sg1-3

!! !!Triggers

%^ConsTransf:0 !!= * @CODE@ 
%^KIN:0        !!= * @CODE@  with ӓвӓ in +PxSg1+Sg+Gen etc.
%^А2ЫӸ:0       !!= * @CODE@  with карта and картын витринӹн

!! !!Boundary symbols

 %> !!= * @CODE@ 
 #  !!  * hash #
 %- !!= * @CODE@ 


 ;


!! !!Sets 

Sets
BackLabVows =   о у ё ю О У Ё Ю ;
FrontLabVows =   ӧ ӱ Ӧ Ӱ;
NonLabVows =   ӓ а е и э я Ӓ А Е И Э Я ;
Vows =  BackLabVows FrontLabVows NonLabVows ы Ы  ӹ Ӹ ; 
BackVows = BackLabVows а А ы Ы ;
FrontVows = FrontLabVows е и э ӓ ӹ Е И Э Ӓ Ӹ ;
ArchiVows = %{ыӹØ%} Ы2 %{аӓ%} ;
FullVows = BackLabVows FrontLabVows NonLabVows ;

CnsNoLN =  б в г д ж з к м ҥ п р с т ф х ц ч ш 
           Б В Г Д Ж З К М Ҥ П Р С Т Ф Х Ц Ч Ш ;


Cns =  б в г д ж з к л м н ҥ п р с т ф х ц ч ш 
       Б В Г Д Ж З К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш ;
Cst =  к п т ф х ц ч ш с 
       К П Т Ф Х Ц Ч Ш Ц ;

Ltrs = Vows Cns Й й  ъ ь Ъ Ь ;

all =  Ltrs  %- ;



!! !!Definitions

Definitions

LFC =  [ :FrontVows | Е2:е | Е2:э | :е | :ӹ | :Ӹ ] \[ #: | :BackVows | :ы | :Ы ]* (%{аӓ%}:я ) \[ #: | :BackVows | :ы | :Ы ]*  (%^А2ЫӸ:0) ;
! # [ Cns | й ]*  [ :FrontVows | Е2: | :е | :ӹ | :Ӹ ] \[ #: | :BackVows | :ы | :Ы ]* (%{аӓ%}:я ) \[ #: | :BackVows | :ы | :Ы ]*  (%^А2ЫӸ:0) ;

LBC = # [ Cns | й ]* [ :BackVows ] \[ #: | :FrontVows | :Ӹ | :ӹ ]* (%{аӓ%}:я) \[ #: | :FrontVows | :Ӹ | :ӹ ]* (%^А2ЫӸ:0) ;
! [ :BackVows ] ( %> | %^ConsTransf:0 | %^KIN:0 | BackVows | %{аӓ%}:а | %{аӓ%}:я | %{ыӹØ%}:ы | Ь2:0 | %{ыӹØ%}:0 | Е3:0 | Ы2:ы | Cns: )* ;


!! !!Rules

Rules

!! !!Palatalisation

"Palatal mark loss before vowel"
!! __@RULENAME@__  
    ь:0  <=>   _ %> А2: ;
         [ н | л ] _ %> [ :я | :е | :и | :ю ]  ;
    
"й Deletion in front of я Suffix and others"
!! __@RULENAME@__  
  й:0 <=> _ [( %^ConsTransf:0 ) %> [ %{аӓе%}: | :я | :е | :и | :ю ] | [ ы:0 | ӹ:0 ] ( %^ConsTransf:0 ) %> [ %{аӓе%}: | %{аӓ%}: | %{ыӹе%}:| е ] ]  ;
    _ %> %{аӓ%}: ;
!€# кай%>%{аӓ%}ш
!€0 ка00яш
         
  
"Ь2:ь Palatal mark for V АМ +Prt1+Sg1"
!! __@RULENAME@__  
Ь2:ь <=> [ н | л ] _ %> %{ыӹØ%}: ;

"Ь2:0 Palatal mark for V АМ +Prt1+Sg1"
!! __@RULENAME@__  
Ь2:0 <=> [ FullVows | й | CnsNoLN: ] (%^ConsTransf:) _ %> [ %{ыӹØ%}: | [н | д] %{аӓ%}: ] ;
         Cns (%^ConsTransf:) _ %> [н | д] %{аӓ%}: ;

"Ь2:ш Palatal mark for V ЕМ +Prt1+Sg1"
!! __@RULENAME@__  
Ь2:ш <=> [ ӹ | ы ] (%^ConsTransf:) _ %> [ %{ыӹØ%}: | [н | д] %{аӓ%}: ] ;



!! !!Vowel rules
"Onset vowel in а"
!! __@RULENAME@__
 %{аӓе%}:а <=> LBC Cns  %>  _ ;

"Onset vowel in ӓ"
!! __@RULENAME@__
 %{аӓе%}:ӓ <=> LFC  Cns  %>  _ ;

"Onset vowel in я"
!! __@RULENAME@__
 %{аӓе%}:я <=> й: %>  _ ;

"Onset vowel in е"
!! __@RULENAME@__
 %{аӓе%}:е <=> [ ы: | ӹ: ] %>  _ ;

"Onset vowel in э"
!! __@RULENAME@__
 %{аӓе%}:э <=> [Vows - [ ы: | ӹ: ]] %>  _ ;


! JMR 2012-09-18
"Onset vowel loss in suffix ыӹ0"
!! __@RULENAME@__  
   %{ыӹØ%}:0 <=> :Vows ( %^KIN:0 ) %> _ ;
   	   _ %> [ е | %{ыӹе%}: | %{ыӹØ%}:ы | %{ыӹØ%}:ӹ ] ;
           %> ж Ы2: %> _  ;
          [ а:ӹ | а:ы ] %^А2ЫӸ:0 %> _ ;



"Onset vowel loss in suffix Е3"
!! __@RULENAME@__  
   Е3:0 <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] %^KIN:0 %> _ ;
           %> ж Ы2: %> _  ;


 "Onset vowel loss in suffix Е2"
!! __@RULENAME@__  
   Е2:0 <=> %> ж Ы2: %> _  Cns ;


"Onset vowel Е2 realized in suffix :е"
!! __@RULENAME@__
  Е2:е <=> [[ Cns - ж: ] (Ы2: |ӹ:|ы:)| й: |[Cns | Vows] ж ] (%^ConsTransf:) %> _ [в|м|н|т|ш|п] ;

"Onset vowel Е2:э after retained vowel"
!! __@RULENAME@__  
Е2:э <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] (%^ConsTransf:) %> _ [в|м|н|т|ш|п] ;

"Onset vowel Е3:э after retained vowel"
!! __@RULENAME@__  
Е3:э <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] %> _ ;

"Onset vowel е:э after retained vowel"
!! __@RULENAME@__
е:э <=> [ ю | ӱ | у | ё | о | ӧ | и | е | э | а | ӓ ] %> _ ;
!!= @CODE@ The three onset vowels need checking 2014-01-10

"Stem final ы loss before Е2"
!! __@RULENAME@__  
ы:0 <=> #: Cns* Vows Cns+ _ %>  Е2: [в|м|н|т|ш] ;
           _ (%^ConsTransf:) %> [ %{аӓе%}: | %{ыӹе%}: | е | %{аӓ%}: [м|т|ш|%>|#] | Е2: [в|м|н|т|ш] | Е3: ] ;
        [ а | ы ] %> _ ;
!€# карта%>ы%>ын
!€0 карт00ы00н

"Stem final ӹ loss before Е2"
!! __@RULENAME@__  
ӹ:0 <=> #: Cns* Vows Cns+ _ %> Е2: [в|м|н|т|ш] ;
           _ (%^ConsTransf: ) %> [ %{аӓе%}: | %{ыӹе%}: | е | %{аӓ%}: [м|т|ш|%>|#]| Е2: [в|м|н|т|ш] | Е3: ] ;
        [ ӓ | ӹ ] %> _ ;
!€# бирка%>ӹ%>ӹн
!€0 бирк00ӹ00н

!! !!Vowel harmony

"Onset vowel ыӹ0 realized in suffix  %{ыӹØ%}:ы"
!! __@RULENAME@__  
  %{ыӹØ%}:ы <=> [ LBC  Cns  (( (ы) Ь2:) %> )| LBC ( н | л ) (ы) Ь2: %> | BackVows й: ( (ы) Ь2:0 ) %> | :BackVows Cns* | [ :BackVows | LBC ] [ Cns+ | Cns* й ] [ %{ыӹØ%}:0 | ы:0 ] %> ] _ [ ( %> ) [ Cns ] | # ] ;
                 
! [[ Cns | й ]+ %> | %> [ %> | BackVows | е | ArchiVows: | Cns ]* [ Cns | й ] ( %> ) ] _ [ ( %> ) Cns | # ] ;

"Onset vowel %{ыӹØ%} realized in suffix  %{ыӹØ%}:ӹ"
!! __@RULENAME@__
  %{ыӹØ%}:ӹ <=> [ LFC [ Cns  | й ]  ( (ӹ) Ь2: ) | :FrontVows Cns+ | [ :FrontVows | LFC ] [ Cns+ | Cns* й ] [ %{ыӹØ%}:0 | ӹ:0 ] ( (ӹ) Ь2:) ] %> _ [ ( %> ) [ Cns  ] | # ] ;
                 [ :FrontVows :0* ]  н _ т ;
!                 :FrontVows  н _ т ;
                  LFC [ ш | м ] _ [ ( %> ) [ Cns  ] | # ] ;

"Onset vowel %{ыӹе%} realized in suffix  %{ыӹе%}:ӹ"
!! __@RULENAME@__
%{ыӹе%}:ӹ <=> LFC [ Cns | й ] %> _  ;

"Onset vowel %{ыӹе%} realized in suffix  %{ыӹе%}:ы"
!! __@RULENAME@__
%{ыӹе%}:ы <=> LBC [ Cns | й ] %> _  ;

"Onset vowel %{ыӹе%} realized in suffix  %{ыӹе%}:е"
!! __@RULENAME@__
%{ыӹе%}:е <=> [ Cns: | й: ] [ ы:0 | ӹ:0 | %{ыӹØ%}: ]  %> _  ;
! [ ы: | ӹ: | %{ыӹØ%}: ]  %> _  ;
        
"Onset vowel %{ыӹе%} realized in suffix  %{ыӹе%}:э"
!! __@RULENAME@__
%{ыӹе%}:э <=>  Vows   %> _  ;

"Onset vowel %{ыӹэ%} realized in Ine and Ill suffixes %{ыӹэ%}:0"
!! __@RULENAME@__
 %{ыӹэ%}:0 <=> [ ы | ӹ ] %> _  ;
!€# комбы%>%{ыӹэ%}штем
!€0 комбы00штем

"Onset vowel %{ыӹэ%} realized in Ine and Ill suffixes %{ыӹэ%}:0"
!! __@RULENAME@__
 %{ыӹэ%}:э <=> [ а | ӓ | я | о ] %> _  ;
!€# олма%>%{ыӹэ%}штем
!€0 олма0эштем


!%{аӓ%}:я <=> Vows й:0 (ы:0|ӹ:0) %> _ [м|т|ш|%>|#] ;
"Affix mid or final front %{аӓ%}:ӓ"
!! __@RULENAME@__
 %{аӓ%}:ӓ <=> [ LFC (ӹ:) [ :Cns (%>)| %> Cns:+ ] | :FrontVows Cns* %> ( %{ыӹØ%}: м ) | %> [ Е2:е | %{ыӹе%}:е | %{ыӹе%}:э | %{ыӹе%}:ӹ | %{ыӹе%}: м %{аӓ%}:ӓ ] [в|д|м|н|т|ш] Cns:* | Cns ӹ: %> ] _ [д|м|н|т|ш|к|%>|#] ;
      [е [в|д|м|н|т|ш]|ӓ] %> Cns* _ [д|м|н|т|ш|к|%>|#] ;
!€# комбы%>еш%>н%{аӓ%}
!€0 комб00еш0нӓ
          :FrontVows Cns* %> р _ к ;
          FrontVows  ӹ: %> _ # ;
          [ :FrontVows | :FrontLabVows ] [ :0 - й:0 ]* %> Cns* _ ;
! [ LFC (ӹ:) [ :Cns (%>)| %> Cns:+ ] | :FrontVows Cns* %> ( %{ыӹØ%}: м ) | %> [ Е2:е | %{ыӹе%}:е | %{ыӹе%}:ӹ | %{ыӹе%}: м %{аӓ%}:ӓ ] [в|д|м|н|т|ш] Cns:* | Cns ӹ: %> ] _ [д|м|н|т|ш|%>|#] ;
!€# библиотека%^А2ЫӸ%>л%{аӓ%}н
!€0 библиотек0ӹ0лӓн

"Affix mid or final back %{аӓ%}:а"
!! __@RULENAME@__  
 %{аӓ%}:а <=> [ :BackVows  Cns*  %> ( %{ыӹØ%}: м | %{ыӹе%}:ы м ( %{аӓ%}:а д ) | %{ыӹе%}:ы [н|д] ) | :BackVows й %> [ %{ыӹØ%}: м | %{ыӹе%}:ы м ( %{аӓ%}:а д ) | %{ыӹе%}:ы [н|д] ] | LBC [ й %> %{ыӹØ%}: м  | :Cns  %> |р|н|д|л]| Cns ы: %> ] _ [д|м|н|т|ш|к|%>|#];
          [ :BackVows | :BackLabVows ] [ :0 - й:0 ]* %> Cns* _ ;
          BackVows  й: %^ConsTransf:0 %> [ н | д ] _ [м|т|%>|#];
!€# колт>%{ыӹØ%}д%{аӓ%}
!€0 колт0ыда
  # я Cns:+ %> _ ;
!€# яд%>%{аӓ%}ш
!€0 яд0аш

"Affix mid or final back %{аӓ%}:я"
!! __@RULENAME@__  
 %{аӓ%}:я <=> Vows й: (ы:0|ӹ:0) %> _ [м|т|ш|%>|#] ;
!€# кай%>%{аӓ%}ш
!€0 ка00яш
          Cns ь:0 %> _ [м|т|ш|%>|#] ;

"Affix initial back а:я"
!! __@RULENAME@__  
 а:я <=> й:0 %> _ \[ %^А2ЫӸ: ] ;

"Not SgNom а:ы"
!! __@RULENAME@__
а:ы <=> BackVows Cns+ _ %^А2ЫӸ:0 %> \[ е ] ;

"Not SgNom а:ӹ"
!! __@RULENAME@__
а:ӹ <=> FrontVows Cns+ _ %^А2ЫӸ:0 %> \[ е ] ;


"suffix-final vowel backed Ы2ы"
!! __@RULENAME@__  
 Ы2:ы <=> LBC Cns _ [ м %{ыӹØ%}: | %>  (%{ыӹØ%}:) Cns: | # ] ;
          [LBC (й ( %^ConsTransf:0 ))| а:ы %^А2ЫӸ:0 ] %> [ (0:ы) ж | ш т | %{ыӹØ%}: ш [ т | к ] ] _ [ %> (%{ыӹØ%}:) Cns:  | # ] ;
!  LBC %> [ ж: | д Ы2: м | ( %{ыӹØ%}: ) м | ( %{ыӹØ%}: ) ш ] _ [ #: | %> [ л | г | ш [ т | к ] | д | %{ыӹØ%}: [ м | н ] ]] ;
!          LBC %> д _ м Ы2: ;
! Work 2012-10-29            %>  %{ыӹØ%}: ш [ т | к ]  _ %> %{ыӹØ%}: ;


"suffix-final vowel fronted Ы2:ӹ"
!! __@RULENAME@__  
 Ы2:ӹ <=> LFC Cns _ [ м %{ыӹØ%}: | %> (%{ыӹØ%}:) Cns: | # ] ;
          [LFC (й ( %^ConsTransf:0 ))| а:ӹ %^А2ЫӸ:0 | FrontVows [ш] ] %> [ (0:ӹ ) ж | ш т | %{ыӹØ%}: ш [ т | к ] ] _ [ %> (%{ыӹØ%}:) Cns:  | # ] ;
! LFC Cns _ [ %> :0* Cns: | # ] ;
!          LFC й ( %^ConsTransf:0 ) %> [ ж | ш т ] _ [ %>  :0* Cns:  | # ] ;

!! !!Consonant loss or transformation

"т:0 in am type verbs" 
!! __@RULENAME@__ лӓктӓш:лӓкнӓ
т:0 <=> к _ %^ConsTransf:0 ;

"к:0 in am type verbs" 
!! __@RULENAME@__ качкаш:качна
к:0 <=> [ ш | ч ] _ %^ConsTransf:0 ; 

"з:ц in am type verbs" 
!! __@RULENAME@__ вазаш:вацна
з:ц <=> [ Vows | н: ] _ %^ConsTransf:0 ;

"н:0 before з:ц in am type verbs" 
!! __@RULENAME@__ негӹнзӓш:негӹц
н:0 <=>  _ з: %^ConsTransf:0 ;

!! !!Vowel again

!! !Vowel gain
"0:ы between ш _ ж"
!! __@RULENAME@__ йиш:йишӹжӹ
0:ы <=> [ LBC | BackVows ]  ш %> _ ж Ы2: ;

"0:ӹ between ш _ ж"
!! __@RULENAME@__ йиш:йишӹжӹ
0:ӹ <=> [ LFC | FrontVows ] ш %> _ ж Ы2: ;


!! !Vowel loss
"suffix-final vowel loss before subsequent suffix-initial vowel Ы2:0" 
!! __@RULENAME@__  
  Ы2:0 <=>    Cns %> [ %{ыӹØ%}: [ д Ы2: м | м | ш ( т | к ) ]| (0:ы|0:ӹ) ж: ] _ %> [ Е2: [в|м|н|т|ш] | А2: ] ;
! Vows %> [ %{ыӹØ%}: ш [ т | к ] | ж: ] _ %> [ л | г | %{ыӹØ%}: ш | ш | д | Е2: [в|м|н|т|ш] | А2: ] ;

"Not SgNom for lat а:0"
!! __@RULENAME@__
а:0 <=> Cns:+ _ %^А2ЫӸ:0 %> е  ;
        # [ Cns: | Vows: | %> ]+ Cns:+ _ %> [ ӹ: | ы: ] %> ;            


!! !!Devoicing
"Onset consonant devoicing" 
!! __@RULENAME@__  
 ж:ш <=> [ :Cst ( :0 ) | ж ] ( %^ConsTransf:0 (Ь2: ) ) %> _ [ %{ыӹØ%}: | Ы2: ] ;



!! !!DISALLOW

!"Disallow Е2:0 after Letters"
!!!! __@RULENAME@__  
!Е2:0 /<= Ltrs _ ;
