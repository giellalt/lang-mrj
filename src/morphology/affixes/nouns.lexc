!! !!!Noun inflection
!! ----

!! Hill Mari nouns inflect in cases.

LEXICON NTag  !!≈ * __@CODE@__ 
+N: NMN_OBLIQUE-INDEFINITE ;
+N: NMN_OBLIQUE-POSSESSA ;
+N+Sg+Nom: 	  K	; 

!! !!Substandard

LEXICON N_КОЛ_SUB !!≈ * __@CODE@__ 
+Err/Sub: N_КОЛ ;

LEXICON N_ТӸРВӸ_SUB !!≈ * __@CODE@__ 
+Err/Sub: N_ТӸРВӸ ;

!! !!Standard

LEXICON N_КОЛ !!= @CODE@ кол:кол
 N_КОЛ-SG ;
 N_КОЛ-PL ;
LEXICON N_КОЛ-SG
+N: NMN_КОЛ-SG ;
LEXICON N_КОЛ-PL
+N: NMN_КОЛ-PL ;

LEXICON N_ТӸРВӸ !!≈ * __@CODE@__ тӹрвӹ:тӹрвӹ
!! The stem vowel "е" is found with possessor indices and the lative
 N_ТӸРВӸ-SG ;
 N_ТӸРВӸ-PL ;
LEXICON N_ТӸРВӸ-SG
+N: NMN_ТӸРВӸ-SG ;
LEXICON N_ТӸРВӸ-PL
+N: NMN_ТӸРВӸ-PL ;

LEXICON N_КАРТА_SUB
+Err/Sub: N_КАРТА ;

LEXICON N_КАРТА !!≈ * __@CODE@__ карта:карта 
!! а is specific to the +Sg+Nom, elsewhere ы or ӹ
 N_КАРТА-SG ;
 N_КАРТА-PL ;
LEXICON N_КАРТА-SG
+N: NMN_КАРТА-SG ;
LEXICON N_КАРТА-PL
+N: NMN_КАРТА-PL ;

LEXICON N_ПОЧТА !!= @CODE@  почта:почта
+N: NMN_OBLIQUE-INDEFINITE ;
+N: NMN_OBLIQUE-POSSESSA ;
+N+Sg+Nom: 	  K	; 

LEXICON N_ОЛМА_SUB
+Err/Sub: N_ОЛМА ;
LEXICON N_ОЛМА !!= @CODE@  олма:олма
 N_ОЛМА-SG ;
 N_ОЛМА-PL ;
LEXICON N_ОЛМА-SG
+N: NMN_ОЛМА-SG ;
LEXICON N_ОЛМА-PL
+N: NMN_ОЛМА-PL ;

!!
LEXICON NMN_КОЛ !!= @CODE@ кол:кол
 NMN_КОЛ-SG ;
 NMN_КОЛ-PL ;
LEXICON NMN_КОЛ-SG
+Sg+Nom: 	  K	; 
 NMN_OBLIQUE-INDEFINITE-SG ;
 NMN_OBLIQUE-POSSESSA-SG ;

LEXICON NMN_КОЛ-PL
 NMN_OBLIQUE-INDEFINITE-PL ;
 NMN_OBLIQUE-POSSESSA-PL ;

LEXICON NMN_ТӸРВӸ !!≈ * __@CODE@__ тӹрвӹ:тӹрвӹ
!! The stem vowel "е" is found with possessor indices and the lative
 NMN_ТӸРВӸ-SG ;
 NMN_ТӸРВӸ-PL ;
LEXICON NMN_ТӸРВӸ-SG
+Sg+Nom:          K     ;
 NMN_OBLIQUE-INDEFINITE-SG ;
 NMN_OBLIQUE-POSSESSA-SG ;
!+PxSg1:%>Е2м SG-ABEACCCOMDATGEN ;

LEXICON NMN_ТӸРВӸ-PL
 NMN_OBLIQUE-INDEFINITE-PL ;
 NMN_OBLIQUE-POSSESSA-PL ;
+PxSg1:эм PL-ABEACCCOMDATGENILLINELATNOM ;

LEXICON NMN_КАРТА	 !!≈ * __@CODE@__ карта:карта
!! а is specific to the +Sg+Nom, elsewhere ы or ӹ
 NMN_КАРТА-SG ;
 NMN_КАРТА-PL ;
LEXICON NMN_КАРТА-SG
+Sg+Nom:          K     ;
:%^А2ЫӸ NMN_OBLIQUE-INDEFINITE-SG ;
:%^А2ЫӸ NMN_OBLIQUE-POSSESSA-SG ;

LEXICON NMN_КАРТА-PL
:%^А2ЫӸ NMN_OBLIQUE-INDEFINITE-PL ;
:%^А2ЫӸ NMN_OBLIQUE-POSSESSA-PL ;

LEXICON NMN_ОЛМА !!= @CODE@  олма:олма
 NMN_ОЛМА-SG ;
 NMN_ОЛМА-PL ;
LEXICON NMN_ОЛМА-SG
+Sg+Nom:          K     ;
 NMN_OBLIQUE-INDEFINITE-SG ;
 NMN_OBLIQUE-POSSESSA-SG ;
+PxSg1+Sg+Nom:эм K ;
+PxSg2+Sg+Nom:ет K ;
+Sg+Lat+PxSg3:%>еш%{ыӹØ%}ж%{ыӹØ%}	   K  ;

LEXICON NMN_ОЛМА-PL
 NMN_OBLIQUE-INDEFINITE-PL ;
 NMN_OBLIQUE-POSSESSA-PL ;

+PxSg1+Pl+Nom:эмвлӓ K ;
+PxSg1+Pl+Nom:мвлӓ K ;
+Pl+PxSg1+Nom:влӓэм K ;
+PxSg2+Pl+Nom:етвлӓ K ;
+PxSg2+Pl+Nom:твлӓ K ;
+Pl+Nom+PxSg2:влӓэт K ;

LEXICON N_ӒВӒ !!= @CODE@  ӓвӓ:ӓвӓ
+N: NMN_OBLIQUE-INDEFINITE ;
+N:%^KIN NMN_OBLIQUE-POSSESSA ;
+N+Sg+Nom: 	  K	; 
+N+PxSg1+Sg+Nom:эм K ;
!!2013-10-18!+N+PxSg1+Sg+Nom:м K ; !! Special
+N+PxSg1+Pl+Nom:эмвлӓ K ;
!!2013-10-18!+N+PxSg1+Pl+Nom:мвлӓ K ;
!!2013-10-18!+N+Pl+PxSg1+Nom:влӓэм K ;
+N+PxSg2+Sg+Nom:ет K ;
!!2013-10-18!+N+PxSg2+Sg+Nom:т K ; !! Special
+N+PxSg2+Pl+Nom:етвлӓ K ;
!!2013-10-18!+N+PxSg2+Pl+Nom:твлӓ K ;
!!2013-10-18!+N+Pl+Nom+PxSg2:влӓэт K ;
!!2013-10-18!+N+Sg+Lat+PxSg3:%>эш%{ыӹØ%}ж%{ыӹØ%}	   K  ;

!!
 
LEXICON SG-ABEACCCOMDATGEN
+Sg+Abe:де K ;
+Sg+Acc:%>%{ыӹØ%}м K ;
+Sg+Com:ге K ;
+Sg+Comp:%>л%{аӓ%} K ;
+Sg+Dat:%>л%{аӓ%}н K ;
+Sg+Gen:%>%{ыӹØ%}н K ;

LEXICON PL-ABEACCCOMDATGENILLINELATNOM
+Pl+Abe:влӓде K ;
+Pl+Acc:%>влӓм K ;
+Pl+Com:%>влӓге K ;
+Pl+Comp:%>влӓлӓ K ;
+Pl+Dat:%>влӓлӓн K ;
+Pl+Gen:%>влӓн K ;
+Pl+Ill:%>влӓшкӹ K ;
+Pl+Ill:%>влӓш K ;
+Pl+Ine:%>влӓштӹ K ;
+Pl+Lat:%>влӓэш K ;
+Pl+Nom:%>влӓ K ;

LEXICON NMN_OBLIQUE-INDEFINITE
 NMN_OBLIQUE-INDEFINITE-SG ;
 NMN_OBLIQUE-INDEFINITE-PL ;
LEXICON NMN_OBLIQUE-INDEFINITE-SG
+Sg+Gen:%>%{ыӹØ%}н K ; ! 
+Sg+Acc:%>%{ыӹØ%}м K ; !
+Sg+Com:%>ге K ; ! (with a village)
+Sg+Dat:%>л%{аӓ%}н K ; !
+Sg+Ine:%>%{ыӹØ%}штЫ2 K ; ! (in a village)
+Sg+Ill:%>%{ыӹØ%}шкЫ2 K ; ! (to a village)
+Sg+Ill:%>%{ыӹØ%}ш K ; ! (to a village)
+Sg+Lat:%>еш K ; ! (into a village)
+Sg+Comp:%>л%{аӓ%} K ; ! (like a village)
+Sg+Abe:%>де K ; ! (without a village)

LEXICON NMN_OBLIQUE-INDEFINITE-PL
!!
+Pl+Nom:%>влӓ 	  K	; 
+Pl+Gen:%>влӓн K ; ! 
+Pl+Acc:%>влӓм K ; !
+Pl+Com:%>влӓге K ; ! (with a village)
+Pl+Dat:%>влӓлӓн K ; !
+Pl+Ine:%>влӓштӹ K ; ! (in a village)
+Pl+Ill:%>влӓшкӹ K ; ! (to a village)
+Pl+Ill:%>влӓш K ; ! (to a village)
+Pl+Lat:%>влӓэш K ; ! (into a village) 
+Pl+Comp:%>влӓлӓ K ; ! (like a village)
+Pl+Abe:%>влӓде K ; ! (without a village)



LEXICON N_01
+Sg+Gen:%>%{ыӹØ%}н K ; ! 
+Sg+Acc:%>%{ыӹØ%}м K ; !
+Sg+Com:%>ге K ; ! (with a village)
+Sg+Dat:%>л%{аӓ%}н K ; !
+Sg+Ine:%>%{ыӹØ%}шт%{ыӹØ%} K ; ! (in a village)
+Sg+Ill:%>%{ыӹØ%}шк%{ыӹØ%} K ; ! (to a village)
+Sg+Ill:%>%{ыӹØ%}ш K ; ! (to a village)
+Sg+Lat:%>еш K ; ! (into a village)
+Sg+Comp:%>л%{аӓ%} K ; ! (like a village)
+Sg+Abe:%>де K ; ! (without a village)
!!
+Pl+Nom:влӓ 	  K	; 
+Pl+Gen:влӓ%>%{ыӹØ%}н K ; ! 
+Pl+Acc:влӓ%>%{ыӹØ%}м K ; !
+Pl+Com:влӓ%>ге K ; ! (with a village)
+Pl+Dat:влӓ%>л%{аӓ%}н K ; !
+Pl+Ine:влӓ%>%{ыӹØ%}шт%{ыӹØ%} K ; ! (in a village)
+Pl+Ill:влӓ%>%{ыӹØ%}шк%{ыӹØ%} K ; ! (to a village)
+Pl+Ill:влӓ%>%{ыӹØ%}ш K ; ! (to a village)
+Pl+Lat:влӓ%>Е2ш K ; ! (into a village) 
+Pl+Comp:влӓ%>л%{аӓ%} K ; ! (like a village)
+Pl+Abe:влӓ%>де K ; ! (without a village)

LEXICON NMN_OBLIQUE-POSSESSA
 NMN_OBLIQUE-POSSESSA-SG ;
 NMN_OBLIQUE-POSSESSA-PL ;

LEXICON NMN_OBLIQUE-POSSESSA-SG
!! PxSg1+NB+CASE
!! singular possessa
!!

!!
!     кол+N+PxSg1+Sg+Nom: [колем, колэм] # х2 колэмжӹ

!!PxSg1
 PXSG1-SG-ABE 		   ;
 PXSG1-SG-ACC 		   ;
 PXSG1-SG-COM 		   ;
 PXSG1-SG-COMP 		   ;
 PXSG1-SG-DAT		   ;
 PXSG1-SG-GEN 		   ;
 PXSG1-SG-ILL		   ;
 PXSG1-SG-INE		   ;
 PXSG1-SG-LAT		   ;
 PXSG1-SG-NOM 		   ;
!!PxSg2
 PXSG2-SG-ABE 		   ;
 PXSG2-SG-ACC 		   ;
 PXSG2-SG-COM 		   ;
 PXSG2-SG-COMP 		   ;
 PXSG2-SG-DAT		   ;
 PXSG2-SG-GEN 		   ;
 PXSG2-SG-ILL		   ;
 PXSG2-SG-INE		   ;
 PXSG2-SG-LAT		   ;
 SG-NOM-PXSG2 		   ;
!!PxSg3
 PXSG3-SG-ABE 		   ;
 PXSG3-SG-ACC 		   ;
 PXSG3-SG-COM 		   ;
 PXSG3-SG-COMP 		   ;
 PXSG3-SG-DAT		   ;
 PXSG3-SG-GEN 		   ;
 PXSG3-SG-ILL		   ;
 PXSG3-SG-INE		   ;
 PXSG3-SG-LAT		   ;
 PXSG3-SG-NOM 		   ;
!!PxPl1
 PXPL1-SG-ABE 		   ;
 PXPL1-SG-ACC 		   ;
 PXPL1-SG-COM 		   ;
 PXPL1-SG-COMP 		   ;
 PXPL1-SG-DAT		   ;
 PXPL1-SG-GEN 		   ;
 PXPL1-SG-ILL		   ;
 PXPL1-SG-INE		   ;
 PXPL1-SG-LAT		   ;
 PXPL1-SG-NOM 		   ;
!!PxPl2
 PXPL2-SG-ABE 		   ;
 PXPL2-SG-ACC 		   ;
 PXPL2-SG-COM 		   ;
 PXPL2-SG-COMP 		   ;
 PXPL2-SG-DAT		   ;
 PXPL2-SG-GEN 		   ;
 PXPL2-SG-ILL		   ;
 PXPL2-SG-INE		   ;
 PXPL2-SG-LAT		   ;
 SG-NOM-PXPL2 		   ;
!!PxPl3
 PXPL3-SG-ABE 		   ;
 PXPL3-SG-ACC 		   ;
 PXPL3-SG-COM 		   ;
 PXPL3-SG-COMP 		   ;
 PXPL3-SG-DAT		   ;
 PXPL3-SG-GEN 		   ;
 PXPL3-SG-ILL		   ;
 PXPL3-SG-INE		   ;
 PXPL3-SG-LAT		   ;
 PXPL3-SG-NOM 		   ;

LEXICON NMN_OBLIQUE-POSSESSA-PL
! plural possessa

!!PxSg1
 PXSG1-PL-ABE 		   ;
 PXSG1-PL-ACC 		   ;
 PXSG1-PL-COM 		   ;
 PXSG1-PL-COMP 		   ;
 PXSG1-PL-DAT		   ;
 PXSG1-PL-GEN 		   ;
 PXSG1-PL-ILL		   ;
 PXSG1-PL-INE		   ;
 PXSG1-PL-LAT		   ;
 PXSG1-PL-NOM 		   ;

!!PxSg2
 PL-ABE-PXSG2		   ;
 PL-ACC-PXSG2		   ;
 PL-COM-PXSG2		   ;
 PL-COMP-PXSG2 		   ;
 PL-DAT-PXSG2		   ;
 PL-GEN-PXSG2		   ;
 PL-ILL-PXSG2		   ;
 PL-INE-PXSG2		   ;
 PL-LAT-PXSG2		   ;
 PL-NOM-PXSG2		   ;

!!PxSg3
 PXSG3-PL-ABE 		   ;
 PXSG3-PL-ACC 		   ;
 PXSG3-PL-COM 		   ;
 PXSG3-PL-COMP 		   ;
 PXSG3-PL-DAT		   ;
 PXSG3-PL-GEN 		   ;
 PXSG3-PL-ILL		   ;
 PXSG3-PL-INE		   ;
 PXSG3-PL-LAT		   ;
 PXSG3-PL-NOM 		   ;

!!PxPl1
 PXPL1-PL-ABE 		   ;
 PXPL1-PL-ACC 		   ;
 PXPL1-PL-COM 		   ;
 PXPL1-PL-COMP 		   ;
 PXPL1-PL-DAT		   ;
 PXPL1-PL-GEN 		   ;
 PXPL1-PL-ILL		   ;
 PXPL1-PL-INE		   ;
 PXPL1-PL-LAT		   ;
 PXPL1-PL-NOM 		   ;

!!PxPl2
 PXPL2-PL-ABE 		   ;
 PXPL2-PL-ACC 		   ;
 PXPL2-PL-COM 		   ;
 PXPL2-PL-COMP 		   ;
 PXPL2-PL-DAT		   ;
 PXPL2-PL-GEN 		   ;
 PXPL2-PL-ILL		   ;
 PXPL2-PL-INE		   ;
 PXPL2-PL-LAT		   ;
 PXPL2-PL-NOM 		   ;

!!PxPl3
 PXPL3-PL-ABE 		   ;
 PXPL3-PL-ACC 		   ;
 PXPL3-PL-COM 		   ;
 PXPL3-PL-COMP 		   ;
 PXPL3-PL-DAT		   ;
 PXPL3-PL-GEN 		   ;
 PXPL3-PL-ILL		   ;
 PXPL3-PL-INE		   ;
 PXPL3-PL-LAT		   ;
 PXPL3-PL-NOM 		   ;


!!
LEXICON PXSG1-SG-ABE 
+Sg+Abe+PxSg1:%>деэм	   K  ;
+Sg+PxSg1+Abe:%>емде	   K  ;

LEXICON PXSG1-SG-ACC 
+Sg+PxSg1+Acc:%>емӹм K ;

LEXICON PXSG1-SG-COM 
+Sg+PxSg1+Com:%>емге K ;

LEXICON PXSG1-SG-COMP
+Sg+PxSg1+Comp:%>емлӓ K ;
+Sg+Comp+PxSg1:%>л%{аӓ%}%>эм      K  ;

LEXICON PXSG1-SG-DAT 
+Sg+PxSg1+Dat:%>емлӓн K ;
+Sg+Dat+PxSg1:%>л%{аӓ%}нем K ;

LEXICON PXSG1-SG-GEN 
+Sg+PxSg1+Gen:%>емӹн K ;

LEXICON PXSG1-SG-ILL 
+Sg+Ill+PxSg1:%>%{ыӹØ%}шкем K ;
+Sg+PxSg1+Ill:%>емӹшкӹ	   K  ;

LEXICON PXSG1-SG-INE 
+Sg+PxSg1+Ine:%>емӹштЫ2	   K  ;
+Sg+Ine+PxSg1:%>%{ыӹØ%}штем K ;

LEXICON PXSG1-SG-LAT 
+Sg+Lat+PxSg1:%>ешем K ;
+Sg+PxSg1+Lat:%>емӹш	   K  ;

LEXICON PXSG1-SG-NOM 
+Sg+PxSg1+Nom:%>ем K ;

LEXICON PXSG1-PL-ABE 
+Pl+PxSg1+Abe:%>влӓэмде	   K  ;
+PxSg1+Pl+Abe:%>емвлӓде   K  ;

LEXICON PXSG1-PL-ACC 
+PxSg1+Pl+Acc:%>емвлӓм	   K  ;
+Pl+PxSg1+Acc:%>влӓэмӹм K ;

LEXICON PXSG1-PL-COM 
+PxSg1+Pl+Com:%>емвлӓге   K  ;
+Pl+PxSg1+Com:%>влӓэмге	   K  ;

LEXICON PXSG1-PL-COMP
+Pl+PxSg1+Comp:%>влӓэмлӓ	   K  ;
+Pl+Comp+PxSg1:%>влӓлӓэм	   K  ;
+PxSg1+Pl+Comp:%>емвлӓлӓ  K  ;

LEXICON PXSG1-PL-DAT 
+Pl+PxSg1+Dat:%>влӓэмлӓн   K  ;
+PxSg1+Pl+Dat:%>емвлӓлӓн  K  ;
+Pl+Dat+PxSg1:%>влӓлӓнем   K  ;
!+PxSg1+Pl+Dat:%>емвлӓлӓ   K  ; !!= * __@CODE@__ Is this really supposed to be here

LEXICON PXSG1-PL-GEN 
+PxSg1+Pl+Gen:%>емвлӓн	   K  ;
+Pl+PxSg1+Gen:%>влӓэмӹн K ;

LEXICON PXSG1-PL-ILL 
+Pl+Ill+PxSg1:%>влӓшкем	   K  ;
+PxSg1+Pl+Ill:%>емвлӓшкӹ  K  ;
+PxSg1+Pl+Ill:%>емвлӓш  K  ;
+Pl+Ill+PxSg1:%>влӓшкем	   K  ;

LEXICON PXSG1-PL-INE 
+Pl+Ine+PxSg1:%>влӓштем    K  ;
+PxSg1+Pl+Ine:%>емвлӓштӹ  K  ;

LEXICON PXSG1-PL-LAT 
+Pl+Lat+PxSg1:%>влӓэшем	   K  ;
+PxSg1+Pl+Lat:%>емвлӓэш   K  ;
+PxSg1+Pl+Lat:%>емвлӓш   K  ;

LEXICON PXSG1-PL-NOM 
+Pl+PxSg1+Nom:%>влӓэм	   K  ;
+PxSg1+Pl+Nom:%>емвлӓ	   K  ;

!!
LEXICON PXSG2-SG-ABE 
+Sg+Abe+PxSg2:%>деэт	   K  ;
+Sg+PxSg2+Abe:%>етде	   K  ;

LEXICON PXSG2-SG-ACC 
+Sg+PxSg2+Acc:%>етӹм	   K  ;

LEXICON PXSG2-SG-COM 
+Sg+PxSg2+Com:%>етге	   K  ;

LEXICON PXSG2-SG-COMP
+Sg+Comp+PxSg2:%>л%{аӓ%}эт      K  ;
+Sg+PxSg2+Comp:%>етлӓ     K  ;

LEXICON PXSG2-SG-DAT 
+Sg+PxSg2+Dat:%>етлӓн	   K  ;

LEXICON PXSG2-SG-GEN 
+Sg+PxSg2+Gen:%>етӹн	   K  ;

LEXICON PXSG2-SG-ILL 
+Sg+Ill+PxSg2:%>%{ыӹØ%}шкет	   K  ;
+Sg+PxSg2+Ill:%>етӹшкӹ	   K  ;
+Sg+PxSg2+Ill:%>етӹш	   K  ;

LEXICON PXSG2-SG-INE 
+Sg+Ine+PxSg2:%>%{ыӹØ%}штет	   K  ;
+Sg+PxSg2+Ine:%>етӹштӹ	   K  ;

LEXICON PXSG2-SG-LAT 
+Sg+Lat+PxSg2:%>ешет	   K  ;
+Sg+PxSg2+Lat:%>етӹш	   K  ;

LEXICON SG-NOM-PXSG2 
+Sg+PxSg2+Nom:%>ет		   K  ;

LEXICON PL-ABE-PXSG2 
+Pl+PxSg2+Abe:%>влӓэтде	   K  ;
+PxSg2+Pl+Abe:%>етвлӓде   K  ;

LEXICON PL-ACC-PXSG2 
+PxSg2+Pl+Acc:%>етвлӓм	   K  ;
+Pl+PxSg2+Acc:%>влӓэтӹм	   K  ;

LEXICON PL-COM-PXSG2 
+PxSg2+Pl+Com:%>етвлӓге   K  ;
+Pl+PxSg2+Com:%>влӓэтге	   K  ;

LEXICON PL-COMP-PXSG2
+Pl+PxSg2+Comp:%>влӓэтлӓ	   K  ;
+Pl+Comp+PxSg2:%>влӓлӓэт	   K  ;
+PxSg2+Pl+Comp:%>етвлӓлӓ  K  ;

LEXICON PL-DAT-PXSG2 
+Pl+PxSg2+Dat:%>влӓэтлӓн   K  ;
+PxSg2+Pl+Dat:%>етвлӓлӓн  K  ;
+Pl+Dat+PxSg2:%>влӓлӓнет   K  ;
!+PxSg2+Pl+Dat:%>етвлӓлӓ   K  ; !!= * __@CODE@__ Is this really supposed to be here

LEXICON PL-GEN-PXSG2 
+PxSg2+Pl+Gen:%>етвлӓн	   K  ;
+Pl+PxSg2+Gen:%>влӓэтӹн	   K  ;

LEXICON PL-ILL-PXSG2 
+Pl+Ill+PxSg2:%>влӓшкет	   K  ;
+PxSg2+Pl+Ill:%>етвлӓшкӹ  K  ;
+PxSg2+Pl+Ill:%>етвлӓш  K  ;

LEXICON PL-INE-PXSG2 
+Pl+Ine+PxSg2:%>влӓштет    K  ;
+PxSg2+Pl+Ine:%>етвлӓштӹ  K  ;

LEXICON PL-LAT-PXSG2 
+Pl+Lat+PxSg2:%>влӓэшет	   K  ;
!+PxSg2+Pl+Lat:%>етвлӓэш   K  ;
+PxSg2+Pl+Lat:%>етвлӓэш   K  ;

LEXICON PL-NOM-PXSG2 
+PxSg2+Pl+Nom:%>етвлӓ	   K  ;
+Pl+PxSg2+Nom:%>влӓэт	   K  ;

!! PXSG3
LEXICON PXSG3-SG-ABE
+Sg+Abe+PxSg3:%>де%>жЫ2	   K  ;
+Sg+PxSg3+Abe:%>жЫ2%>де	   K  ;

LEXICON PXSG3-SG-ACC
+Sg+PxSg3+Acc:%>жЫ2%>м	   K  ;

LEXICON PXSG3-SG-COM
+Sg+PxSg3+Com:%>жЫ2%>ге	   K  ;

LEXICON PXSG3-SG-COMP
+Sg+Comp+PxSg3:%>л%{аӓ%}%>жЫ2      K  ;
+Sg+PxSg3+Comp:%>жЫ2%>л%{аӓ%}     K  ;

LEXICON PXSG3-SG-DAT
+Sg+PxSg3+Dat:%>жЫ2%>л%{аӓ%}н	   K  ;

LEXICON PXSG3-SG-GEN
+Sg+PxSg3+Gen:%>жЫ2%>н	   K  ;

LEXICON PXSG3-SG-ILL
+Sg+Ill+PxSg3:%>%{ыӹØ%}шкЫ2%>жЫ2	   K  ;
+Sg+PxSg3+Ill:%>жЫ2%>шкЫ2	   K  ;
+Sg+PxSg3+Ill:%>жЫ2%>ш	   K  ;

LEXICON PXSG3-SG-INE
+Sg+Ine+PxSg3:%>%{ыӹØ%}штЫ2%>жЫ2	   K  ;
+Sg+PxSg3+Ine:%>жЫ2%>штЫ2	   K  ;

LEXICON PXSG3-SG-LAT
+Sg+Lat+PxSg3:%>ешӹжӹ	   K  ;
+Sg+PxSg3+Lat:%>ж%{ыӹØ%}ш	   K  ;

LEXICON PXSG3-SG-NOM
+Sg+PxSg3+Nom:%>жЫ2 	   K  ;

LEXICON PXSG3-PL-ABE
+Pl+PxSg3+Abe:%>влӓжӹде	   K  ;
+PxSg3+Pl+Abe:%>жЫ2%>влӓде   K  ;

LEXICON PXSG3-PL-ACC
+Pl+PxSg3+Acc:%>влӓжӹм	   K  ;
+PxSg3+Pl+Acc:%>жЫ2%>влӓм	   K  ;

LEXICON PXSG3-PL-COM
+Pl+PxSg3+Com:%>влӓжӹге	   K  ;
+PxSg3+Pl+Com:%>жЫ2%>влӓге   K  ;

LEXICON PXSG3-PL-COMP
+Pl+PxSg3+Comp:%>влӓжӹлӓ	   K  ;
+Pl+Comp+PxSg3:%>влӓлӓжӹ	   K  ;
+PxSg3+Pl+Comp:%>жЫ2%>влӓлӓ  K  ;

LEXICON PXSG3-PL-DAT
+Pl+PxSg3+Dat:%>влӓжӹлӓн   K  ;
+Pl+Dat+PxSg3:%>влӓлӓнжӹ   K  ;
+PxSg3+Pl+Dat:%>жЫ2%>влӓлӓн  K  ;
!+PxSg3+Pl+Dat:%>ж%{ыӹØ%}влӓлӓ   K  ; !!= * __@CODE@__ Is this really supposed to be here

LEXICON PXSG3-PL-GEN
+Pl+PxSg3+Gen:%>влӓжӹн	   K  ;
+PxSg3+Pl+Gen:%>жЫ2%>влӓн	   K  ;

LEXICON PXSG3-PL-ILL
+Pl+Ill+PxSg3:%>влӓшкӹжӹ	   K  ;
+Pl+PxSg3+Ill:%>влӓжӹш  K  ;
+Pl+PxSg3+Ill:%>влӓжӹшкӹ  K  ;
+PxSg3+Pl+Ill:%>жЫ2%>влӓшкӹ  K  ;
+PxSg3+Pl+Ill:%>жЫ2%>влӓш   K  ;

LEXICON PXSG3-PL-INE
+Pl+PxSg3+Ine:%>влӓжӹштӹ	   K  ;
+Pl+Ine+PxSg3:%>влӓштӹжӹ    K  ;
+PxSg3+Pl+Ine:%>жЫ2%>влӓштӹ  K  ;

LEXICON PXSG3-PL-LAT
+Pl+Lat+PxSg3:%>влӓэшӹжӹ	   K  ;
+PxSg3+Pl+Lat:%>жЫ2%>влӓэш   K  ;

LEXICON PXSG3-PL-NOM
+Pl+PxSg3+Nom:%>влӓжӹ	   K  ;
+PxSg3+Pl+Nom:%>жЫ2%>влӓ	   K  ;

LEXICON PXPL1-SG-ABE 
+Sg+PxPl1+Abe:%>н%{аӓ%}%>де	K	;

LEXICON PXPL1-SG-ACC 
+Sg+PxPl1+Acc:%>н%{аӓ%}%>%{ыӹØ%}м	K	;

LEXICON PXPL1-SG-COM 
+Sg+PxPl1+Com:%>н%{аӓ%}%>ге	K	;

LEXICON PXPL1-SG-COMP
+Sg+Comp+PxPl1:%>л%{аӓ%}%>н%{аӓ%}       K       ;
+Sg+PxPl1+Comp:%>н%{аӓ%}%>л%{аӓ%}	K	;

LEXICON PXPL1-SG-DAT 
+Sg+PxPl1+Dat:%>н%{аӓ%}%>л%{аӓ%}н	K	;
+Sg+Dat+PxPl1:%>л%{аӓ%}н%>н%{аӓ%}	K	;

LEXICON PXPL1-SG-GEN 
+Sg+PxPl1+Gen:%>н%{аӓ%}%>%{ыӹØ%}н	K	;

LEXICON PXPL1-SG-ILL 
+Sg+Ill+PxPl1:%>%{ыӹØ%}шк%{ыӹØ%}%>н%{аӓ%}	K	;
LEXICON PXPL1-SG-INE 
+Sg+Ine+PxPl1:%>%{ыӹØ%}шт%{ыӹØ%}%>н%{аӓ%}	K	;

LEXICON PXPL1-SG-LAT 
+Sg+Lat+PxPl1:%>еш%>н%{аӓ%}	K	;

LEXICON PXPL1-SG-NOM 
+Sg+PxPl1+Nom:%>н%{аӓ%}	K	;
LEXICON PXPL1-PL-ABE 
+Pl+PxPl1+Abe:%>влӓнӓде	K	;
LEXICON PXPL1-PL-ACC 
+Pl+PxPl1+Acc:%>влӓнӓм	K	;
LEXICON PXPL1-PL-COM 
+Pl+PxPl1+Com:%>влӓнӓге	K	;
LEXICON PXPL1-PL-COMP
+Pl+PxPl1+Comp:%>влӓнӓлӓ	K	;

LEXICON PXPL1-PL-DAT 
+Pl+PxPl1+Dat:%>влӓнӓлӓн	K	;
+Pl+Dat+PxPl1:%>влӓлӓннӓ	K	;

LEXICON PXPL1-PL-GEN 
+Pl+PxPl1+Gen:%>влӓ%>н%{аӓ%}%>%{ыӹØ%}н	K	;
LEXICON PXPL1-PL-ILL 
+Pl+Ill+PxPl1:%>влӓшкӹнӓ	K	;
LEXICON PXPL1-PL-INE 
+Pl+Ine+PxPl1:%>влӓштӹнӓ	K	;
LEXICON PXPL1-PL-LAT 
+Pl+Lat+PxPl1:%>влӓэшнӓ	K	;
LEXICON PXPL1-PL-NOM 
+Pl+PxPl1+Nom:%>влӓ%>н%{аӓ%}	K	;
!!PxPl2
LEXICON PXPL2-SG-ABE 
+Sg+PxPl2+Abe:%>д%{аӓ%}%>де	K	;
LEXICON PXPL2-SG-ACC 
+Sg+PxPl2+Acc:%>д%{аӓ%}%>%{ыӹØ%}м	K	;
LEXICON PXPL2-SG-COM 
+Sg+PxPl2+Com:%>д%{аӓ%}%>ге	K	;
LEXICON PXPL2-SG-COMP
+Sg+Comp+PxPl2:%>л%{аӓ%}%>д%{аӓ%}       K       ;
+Sg+PxPl2+Comp:%>д%{аӓ%}%>л%{аӓ%}	K	;
LEXICON PXPL2-SG-DAT 
+Sg+PxPl2+Dat:%>д%{аӓ%}%>л%{аӓ%}н	K	;
+Sg+Dat+PxPl2:%>л%{аӓ%}н%>д%{аӓ%}	K	;
LEXICON PXPL2-SG-GEN 
+Sg+PxPl2+Gen:%>д%{аӓ%}%>%{ыӹØ%}н	K	;
LEXICON PXPL2-SG-ILL 
+Sg+Ill+PxPl2:%>%{ыӹØ%}шк%{ыӹØ%}%>д%{аӓ%}	K	;
LEXICON PXPL2-SG-INE 
+Sg+Ine+PxPl2:%>%{ыӹØ%}шт%{ыӹØ%}%>д%{аӓ%}	K	;
LEXICON PXPL2-SG-LAT 
+Sg+Lat+PxPl2:%>еш%>д%{аӓ%}	K	;
LEXICON SG-NOM-PXPL2 
+Sg+PxPl2+Nom:%>д%{аӓ%}	K	;
LEXICON PXPL2-PL-ABE
+Pl+PxPl2+Abe:%>влӓ%>д%{аӓ%}%>де	K	;
LEXICON PXPL2-PL-ACC
+Pl+PxPl2+Acc:%>влӓ%>д%{аӓ%}%>%{ыӹØ%}м	K	;
LEXICON PXPL2-PL-COM
+Pl+PxPl2+Com:%>влӓ%>д%{аӓ%}%>ге	K	;
LEXICON PXPL2-PL-COMP
+Pl+PxPl2+Comp:%>влӓ%>д%{аӓ%}%>л%{аӓ%}	K	;
LEXICON PXPL2-PL-DAT
+Pl+PxPl2+Dat:%>влӓ%>дӓлӓн	K	;
+Pl+Dat+PxPl2:%>влӓ%>лӓндӓ	K	;
!+Pl+PxPl2+Dat:%>влӓ%>д%{аӓ%}%>л%{аӓ%}н	K	;
!+Pl+Dat+PxPl2:%>влӓ%>л%{аӓ%}н%>д%{аӓ%}	K	;
LEXICON PXPL2-PL-GEN
+Pl+PxPl2+Gen:%>влӓ%>д%{аӓ%}%>%{ыӹØ%}н	K	;
LEXICON PXPL2-PL-ILL
+Pl+Ill+PxPl2:%>влӓ%>шкӹдӓ	K	;
!+Pl+Ill+PxPl2:%>влӓ%>%{ыӹØ%}шк%{ыӹØ%}%>д%{аӓ%}	K	;
LEXICON PXPL2-PL-INE
+Pl+Ine+PxPl2:%>влӓ%>штӹдӓ	K	;
!+Pl+Ine+PxPl2:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>д%{аӓ%}	K	;
LEXICON PXPL2-PL-LAT
+Pl+Lat+PxPl2:%>влӓэшдӓ	K	;
!+Pl+Lat+PxPl2:%>влӓ%>Е2ш%>д%{аӓ%}	K	;
LEXICON PXPL2-PL-NOM
+Pl+PxPl2+Nom:%>влӓ%>д%{аӓ%}	K	;
!!PxPl3
LEXICON PXPL3-SG-ABE 
+Sg+PxPl3+Abe:%>%{ыӹØ%}шт%{ыӹØ%}%>де	K	;
LEXICON PXPL3-SG-ACC 
+Sg+PxPl3+Acc:%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}м	K	;
LEXICON PXPL3-SG-COM 
+Sg+PxPl3+Com:%>%{ыӹØ%}шт%{ыӹØ%}%>ге	K	;
LEXICON PXPL3-SG-COMP
+Sg+PxPl3+Comp:%>%{ыӹØ%}шт%{ыӹØ%}%>л%{аӓ%}	K	;
LEXICON PXPL3-SG-DAT 
+Sg+PxPl3+Dat:%>%{ыӹØ%}шт%{ыӹØ%}%>л%{аӓ%}н	K	;
+Sg+Dat+PxPl3:%>л%{аӓ%}н%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-SG-GEN 
+Sg+PxPl3+Gen:%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}н	K	;
LEXICON PXPL3-SG-ILL 
+Sg+Ill+PxPl3:%>%{ыӹØ%}шк%{ыӹØ%}%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-SG-INE 
+Sg+Ine+PxPl3:%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-SG-LAT 
+Sg+Lat+PxPl3:%>еш%>ӹштӹ	K	;
!+Sg+Lat+PxPl3:%>еш%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-SG-NOM 
+Sg+PxPl3+Nom:%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-PL-ABE 
+Pl+PxPl3+Abe:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>де	K	;
LEXICON PXPL3-PL-ACC 
+Pl+PxPl3+Acc:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}м	K	;
LEXICON PXPL3-PL-COM 
+Pl+PxPl3+Com:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>ге	K	;
LEXICON PXPL3-PL-COMP
+Pl+PxPl3+Comp:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>л%{аӓ%}	K	;
LEXICON PXPL3-PL-DAT 
+Pl+PxPl3+Dat:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>л%{аӓ%}н	K	;
+Pl+Dat+PxPl3:%>влӓ%>л%{аӓ%}н%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-PL-GEN 
+Pl+PxPl3+Gen:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}н	K	;
LEXICON PXPL3-PL-ILL 
+Pl+Ill+PxPl3:%>влӓ%>%{ыӹØ%}шк%{ыӹØ%}%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-PL-INE 
+Pl+Ine+PxPl3:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-PL-LAT 
+Pl+Lat+PxPl3:%>влӓ%>Е2ш%>%{ыӹØ%}шт%{ыӹØ%}	K	;
LEXICON PXPL3-PL-NOM 
+Pl+PxPl3+Nom:%>влӓ%>%{ыӹØ%}шт%{ыӹØ%}	K	;



! vim: set ft=xfst-lexc:

