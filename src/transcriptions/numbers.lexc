! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

Multichar_Symbols

 +Use/NG    ! Do not generate, for isme-ped.fst and apertium
 +Attr	    ! only in attribute position кок кол, кок колым
 +Card	    !                            кокты, коктым
 +Ord	    ! This will be removed

LEXICON Root
HUNDREDSM ;		! 200M
шӱдӹ% :1 HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 11-19M
TENM ;			! 10M->11M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
шӱдӹ% :1 HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
тӹжем:1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
% : ONES ;			! 1-9

LEXICON HUNDREDSM
кок:2  CUODIM ;
кым:3 CUODIM ;
нӹл:4 CUODIM ;
вӹц:5 CUODIM ;
куд:6 CUODIM ;
шӹм:7 CUODIM ;
кӓндӓкш:8 CUODIM ;
ӹндекш:9 CUODIM ;

LEXICON CUODIM
% шӱдӹ% : HUNDREDM ;

LEXICON HUNDREDM
TENSM ;
TEENSM ;
:%0 ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
:1 TEENM ;

LEXICON TEENM
луат: LOHKAIM ;


LEXICON LOHKAIM
ик:1 MILJON ;
кок:2 MILJON ;
кым:3 MILJON ;
нӹл:4 MILJON ;
вӹц:5 MILJON ;
куд:6 MILJON ;
шӹм:7 MILJON ;
кӓндӓкш:8 MILJON ;
ӹндекш:9 MILJON ;

LEXICON TENSM
коклы:2 LÅGEVM ;
кымлы:3 LÅGEVM ;
нӹллӹ:4 LÅGEVM ;
вӹцлӹ:5 LÅGEVM ;
кудлы:6 LÅGEVM ;
шӹмлӹ:7 LÅGEVM ;
кӓндӓкшлӹ:8 LÅGEVM ;
ӹндекшлӹ:9 LÅGEVM ;

LEXICON TENM
лу:1%0 MILJON ;

LEXICON LÅGEVM
:%0 MILJON ;
% : ONESM ;

LEXICON ONESM
ик% миллион% :1 OVERTHOUSANDS ;
кок:2 MILJON ;
кым:3 MILJON ;
нӹл:4 MILJON ;
вӹц:5 MILJON ;
куд:6 MILJON ;
шӹм:7 MILJON ;
кӓндӓкш:8 MILJON ;
ӹндекш:9 MILJON ; 

LEXICON MILJON
% миллион% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
шӱдӹ:1 HUNDREDT ;	! 100.000.000
:%0 TENST ;		!  20.000.000
:%0 TEENST ;		!  10.000.000
:%0%0 ONEST ;		!   2.000.000
тӹжем:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
% :%0%0%0%0%0 LOHKAI ;
:%0%0%0%0%0%0 ENDLEX ;	!  x000000

LEXICON HUNDREDST
кок:2  CUODIT ;	! 200000-299999
кым:3 CUODIT ;	! 300000-399999
нӹл:4 CUODIT ;	! 400000-499999
вӹц:5 CUODIT ;	! 500000-599999
куд:6 CUODIT ;	! 600000-699999
шӹм:7 CUODIT ;	! 700000-799999
кӓндӓкш:8 CUODIT ;	! 800000-899999
ӹндекш:9 CUODIT ;	! 900000-999999

LEXICON CUODIT
% шӱдӹ: HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
% : TENST ;			! X2XYYY, X10YYY
% : TEENST ;		! X1XYYY
иктӹ:%01 THOUSANDS ;
% :%0 ONEST ;		! XX0YYY
:%0%0 THOUSANDS ;	! X00YYY

LEXICON TEENST
!лу:%0 THOUSANDS ;
луат:1 TEENT ;

LEXICON TEENT
ик:1 THOUSANDS ;
кок:2 THOUSANDS ;
кым:3 THOUSANDS ;
нӹл:4 THOUSANDS ;
вӹц:5 THOUSANDS ;
куд:6 THOUSANDS ;
шӹм:7 THOUSANDS ;
кӓндӓкш:8 THOUSANDS ;
ӹндекш:9 THOUSANDS ;


LEXICON TENST
луатик% тӹжем:1 THOUSAND ;
коклы:2 LÅGEVT ;
кымлы:3 LÅGEVT ;
нӹллӹ:4 LÅGEVT ;
вӹцлӹ:5 LÅGEVT ;
кудлы:6 LÅGEVT ;
шӹмлӹ:7 LÅGEVT ;
кӓндӓкшлӹ:8 LÅGEVT ;
ӹндекшлӹ:9 LÅGEVT ;

LEXICON LÅGEVT
:%0 THOUSANDS ;
: ONEST ;

LEXICON ONEST
кок:2 THOUSANDS ;
кым:3 THOUSANDS ;
нӹл:4 THOUSANDS ;
вӹц:5 THOUSANDS ;
куд:6 THOUSANDS ;
шӹм:7 THOUSANDS ;
кӓндӓкш:8 THOUSANDS ;
ӹндекш:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
% тӹжем% : THOUSAND ;

LEXICON THOUSAND
% : UNDERTHOUSAND ;	   ! 1100-1999
% :%0 TENS ;	   ! 1020-1099
% :%0 TEENS ;	   ! 1010-1019
% :%0%0 LOHKAI ;	   ! 1001-1009
:%0%0%0 ENDLEX ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
шӱдӹ:1 HUNDRED ;

LEXICON HUNDREDS
кок:2  CUODI ;
кым:3 CUODI ;
нӹл:4 CUODI ;
вӹц:5 CUODI ;
куд:6 CUODI ;
шӹм:7 CUODI ;
кӓндӓкш:8 CUODI ;
ӹндекш:9 CUODI ;

LEXICON CUODI
% шӱдӹ: HUNDRED ;

LEXICON HUNDRED
% : TENS ;
% : TEENS ;
% :%0 LOHKAI ;
:%0%0 ENDLEX ;

LEXICON TEENS
луат:1 LOHKAI ;

LEXICON LOHKAI
 CARDINAL-LOHKAI ;
 ORDINAL-LOHKAI ;

LEXICON CARDINAL-LOHKAI
ик+Attr:1 ENDLEX ;
кок+Attr:2 ENDLEX ;
кым+Attr:3 ENDLEX ;
нӹл+Attr:4 ENDLEX ;
вӹц+Attr:5 ENDLEX ;
куд+Attr:6 ENDLEX ;
шӹм+Attr:7 ENDLEX ;
кӓндӓкш+Attr:8 ENDLEX ;
ӹндекш+Attr:9 ENDLEX ;

иктӹ:1 ENDLEX ;
кокты:2 ENDLEX ;
кымыт:3 ENDLEX ;
нӹлӹт:4 ENDLEX ;
вӹзӹт:5 ENDLEX ;
кудыт:6 ENDLEX ;
шӹмӹт:7 ENDLEX ;
кӓндӓкшӹ:8 ENDLEX ;
ӹндекшӹ:9 ENDLEX ;

LEXICON ORDINAL-LOHKAI
икшӹ:1%. ENDLEX ;
кокшы:2%. ENDLEX ;
кымшы:3%. ENDLEX ;
нӹлӹмшӹ:4%. ENDLEX ;
вӹзӹмшӹ:5%. ENDLEX ;
кудымшы:6%. ENDLEX ;
шӹмшӹ:7%. ENDLEX ;
кӓндӓкшӹмшӹ:8%. ENDLEX ;
ӹндекшӹмшӹ:9%. ENDLEX ;

LEXICON TENS
лу:1%0 ENDLEX ;
лушы:1%0%. ENDLEX ; !!= @CODE@ ordinal
коклы:2 LÅGEV ;
кымлы:3 LÅGEV ;
нӹллӹ:4 LÅGEV ;
вӹцлӹ:5 LÅGEV ;
кудлу:6 LÅGEV ;
шӹмлу:7 LÅGEV ;
кӓндӓкшлу:8 LÅGEV ;
ӹндекшлу:9 LÅGEV ;

LEXICON LÅGEV
:%0 ENDLEX ;
шы:%0%. ENDLEX ; !!= @CODE@ ordinal
% :    LOHKAI ;

LEXICON ONES
CARDINAL-LOHKAI ;
 ORDINAL-ONES ;

LEXICON ORDINAL-ONES
пӹтӓришӹ:1%. ENDLEX ;
кокшы:2%. ENDLEX ;
кымшы:3%. ENDLEX ;
нӹлӹмшӹ:4%. ENDLEX ;
вӹзӹмшӹ:5%. ENDLEX ;
кудымшы:6%. ENDLEX ;
шӹмшӹ:7%. ENDLEX ;
кӓндӓкшӹмшӹ:8%. ENDLEX ;
ӹндекшӹмшӹ:9%. ENDLEX ;

LEXICON ENDLEX
 # ;
