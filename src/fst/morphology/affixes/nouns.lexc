!! # Noun inflection
!! ----

!! ## Ad hoc lexica

LEXICON N-continutation ! this lexicon made on model from mhr, please adjust ad lib
 N_ ;
 

LEXICON NTag
  N_ ;

! SNM: ensure there is a space before the jspwiki markup __ when the preceding
! character (ie last of lexicon name) is also an _ char - otherwise one gets a
! jspwiki syntax error.
LEXICON N_  !!≈ * **@CODE@**
! +WORK: K ; ! comment out WORK tag for Korp
K ;

!! ## Substandard lexica

!LEXICON N_KOL_SUB !!≈ * **@CODE@**
! +Err/Orth: N_KOL ;

!LEXICON N_TYERVYE_SUB !!≈ * **@CODE@**
! +Err/Orth: N_TYERVYE ;


LEXICON N-continuation
N_ ; ! to be fixed

!! ## Standard lexica 

LEXICON N_KOL !!= @CODE@ кол:кол
!! Back harmony
  N_KOL-SG ;
  N_KOL-PL ;
LEXICON N_KOL-SG
 :%{back%} NMN_KOL-SG ;
LEXICON N_KOL-PL
 :%{front%} NMN_KOL-PL ;

LEXICON N_MOER !!= @CODE@ мӧр:мӧр
!! Front harmony
  N_MOER-SG ;
  N_MOER-PL ;
LEXICON N_MOER-SG
 :%{front%} NMN_MOER-SG ;
LEXICON N_MOER-PL
 :%{front%} NMN_MOER-PL ;

LEXICON N_MARY !!≈ * **@CODE@** мары:мары
!! The stem vowel "е" is found with possessor indices and the lative
!! Back harmony
  N_MARY-SG ;
  N_MARY-PL ;
LEXICON N_MARY-SG
 :%{back%} NMN_MARY-SG ;
LEXICON N_MARY-PL
 :%{front%} NMN_MARY-PL ;

LEXICON N_TYERVYE !!≈ * **@CODE@** тӹрвӹ:тӹрвӹ
!! The stem vowel "е" is found with possessor indices and the lative
!! Front harmony
  N_TYERVYE-SG ;
  N_TYERVYE-PL ;
LEXICON N_TYERVYE-SG
 :%{front%} NMN_TYERVYE-SG ;
LEXICON N_TYERVYE-PL
 :%{front%} NMN_TYERVYE-PL ;

!LEXICON N_KARTA_SUB
!!! @LEXNAME@
! +Err/Orth: N_KARTA ;

LEXICON N_KARTA !!≈ * **@CODE@** карта:карта
!! а is specific to the +Sg+Nom, elsewhere ы or ӹ
!! Back harmony
  N_KARTA-SG ;
  N_KARTA-PL ;
LEXICON N_KARTA-SG
 :%{back%} NMN_KARTA-SG ;
LEXICON N_KARTA-PL
 :%{front%} NMN_KARTA-PL ;

LEXICON N_BIRZHA !!≈ * **@CODE@** биржа:биржа
!! а is specific to the +Sg+Nom, elsewhere  ӹ
!! Front harmony
  N_BIRZHA-SG ;
  N_BIRZHA-PL ;
LEXICON N_BIRZHA-SG
 :%{front%} NMN_BIRZHA-SG ;
LEXICON N_BIRZHA-PL
 :%{front%} NMN_BIRZHA-PL ;

LEXICON N_VAREN1E !!≈ * **@CODE@** варенье:варенье
!! е is specific to the +Sg+Nom, elsewhere  ӹ
!! Front harmony
  N_VAREN1E-SG ;
  N_VAREN1E-PL ;
LEXICON N_VAREN1E-SG
 :%{front%} NMN_VAREN1E-SG ;
LEXICON N_VAREN1E-PL
 :%{front%} NMN_VAREN1E-PL ;

LEXICON N_POCHTA !!= @CODE@  почта:почта
        :%{back%} NMN_OBLIQUE-INDEFINITE-SG_BACK   ;
        :%{front%} NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
        :%{back%} NMN_OBLIQUE-POSSESSA-SG_BACK     ; !!!!
        :%{front%} NMN_OBLIQUE-POSSESSA-PL_BACK     ;
 +Sg+Nom:%{back%} K                                ;

!LEXICON N_OLMA_SUB
! +Err/Orth: N_OLMA ;
LEXICON N_OLMA !!= @CODE@  олма:олма
!! Back harmony
  N_OLMA-SG ;
  N_OLMA-PL ;
LEXICON N_OLMA-SG
 :%{back%} NMN_OLMA-SG ;
LEXICON N_OLMA-PL
 :%{front%} NMN_OLMA-PL ;

LEXICON N_AEZAE !!= @CODE@  ӓзӓ:ӓзӓ
!! Front harmony
   N_AEZAE-SG ;
   N_AEZAE-PL ;
LEXICON N_AEZAE-SG
 :%{front%} NMN_AEZAE-SG ;
LEXICON N_AEZAE-PL
 :%{front%} NMN_AEZAE-PL ;

LEXICON N_PECHEN1E !!= @CODE@  печенье:печенье
!! Front harmony
  N_PECHEN1E-SG ;
  N_PECHEN1E-PL ;
LEXICON N_PECHEN1E-SG
 :%{front%} NMN_PECHEN1E-SG ;
LEXICON N_PECHEN1E-PL
 :%{front%} NMN_PECHEN1E-PL ;

!!
LEXICON NMN_KOL !!= @CODE@ кол:кол
!! Back harmony
  NMN_KOL-SG ;
  NMN_KOL-PL ;
LEXICON NMN_KOL-SG
 +Sg+Nom: K                              ;
          NMN_OBLIQUE-INDEFINITE-SG_BACK ;
          NMN_OBLIQUE-POSSESSA-SG_BACK   ;

LEXICON NMN_KOL-PL
  NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
  NMN_OBLIQUE-POSSESSA-PL_BACK     ;

LEXICON NMN_MOER !!= @CODE@ мӧр:мӧр
!! Front harmony
  NMN_MOER-SG ;
  NMN_MOER-PL ;
LEXICON NMN_MOER-SG
 +Sg+Nom: K                               ;
          NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
          NMN_OBLIQUE-POSSESSA-SG_FRONT   ;

LEXICON NMN_MOER-PL
  NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
  NMN_OBLIQUE-POSSESSA-PL_FRONT    ;

LEXICON NMN_MARY !!≈ * **@CODE@** мары:мары
!! The stem vowel "е" is found with possessor indices and the lative
!! Back harmony
  NMN_MARY-SG ;
  NMN_MARY-PL ;
LEXICON NMN_MARY-SG
 +Sg+Nom: K                              ;
          NMN_OBLIQUE-INDEFINITE-SG_BACK ;
          NMN_OBLIQUE-POSSESSA-SG_BACK   ;

LEXICON NMN_MARY-PL
            NMN_OBLIQUE-INDEFINITE-PL_MUTUAL      ;
            NMN_OBLIQUE-POSSESSA-PL_BACK          ;
! +PxSg1:эм PL-ABEACCCOMDATGENILLINELATNOM_MUTUAL ;

LEXICON NMN_TYERVYE !!≈ * **@CODE@** тӹрвӹ:тӹрвӹ
!! The stem vowel "е" is found with possessor indices and the lative
!! Front harmony
  NMN_TYERVYE-SG ;
  NMN_TYERVYE-PL ;
LEXICON NMN_TYERVYE-SG
 +Sg+Nom: K                               ;
          NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
          NMN_OBLIQUE-POSSESSA-SG_FRONT   ;

LEXICON NMN_TYERVYE-PL
            NMN_OBLIQUE-INDEFINITE-PL_MUTUAL      ;
            NMN_OBLIQUE-POSSESSA-PL_FRONT         ;
! +PxSg1:эм PL-ABEACCCOMDATGENILLINELATNOM_MUTUAL ;

LEXICON NMN_KARTA	 !!≈ * **@CODE@** карта:карта
!! Back а/ы
!! а is specific to the +Sg+Nom, elsewhere ы
  NMN_KARTA-SG ;
  NMN_KARTA-PL ;
LEXICON NMN_KARTA-SG
 +Sg+Nom:    K                              ;
        :%>ы NMN_OBLIQUE-INDEFINITE-SG_BACK ;
        :%>ы NMN_OBLIQUE-POSSESSA-SG_BACK   ;

LEXICON NMN_KARTA-PL
 :%>ы NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
 :%>ы NMN_OBLIQUE-POSSESSA-PL_BACK     ;

LEXICON NMN_BIRZHA	 !!≈ * **@CODE@** биржа:биржа
!! Front а/ӹ
!! а is specific to the +Sg+Nom, elsewhere ӹ
  NMN_BIRZHA-SG ;
  NMN_BIRZHA-PL ;
LEXICON NMN_BIRZHA-SG
  +Sg+Nom:       K                               ;
         :%>ӹ    NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
!        :%^А2ЫӸ NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
         :%>ӹ    NMN_OBLIQUE-POSSESSA-SG_FRONT   ;
!        :%^А2ЫӸ NMN_OBLIQUE-POSSESSA-SG_FRONT   ;

LEXICON NMN_BIRZHA-PL
 :%>ӹ NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ; !!= * **@CODE@** %>влӓ
 :%>ӹ NMN_OBLIQUE-POSSESSA-PL_FRONT    ;

LEXICON NMN_VAREN1E	 !!≈ * **@CODE@** варенье:варенье
!! Front е/ӹ
!! е is specific to the +Sg+Nom, elsewhere ӹ
  NMN_VAREN1E-SG ;
  NMN_VAREN1E-PL ;
LEXICON NMN_VAREN1E-SG
  +Sg+Nom:       K                               ;
         :%>ӹ    NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
!        :%^А2ЫӸ NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
         :%>ӹ    NMN_OBLIQUE-POSSESSA-SG_FRONT   ;
!        :%^А2ЫӸ NMN_OBLIQUE-POSSESSA-SG_FRONT   ;

LEXICON NMN_VAREN1E-PL
 :%>ӹ NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ; !!= * **@CODE@** %>влӓ
 :%>ӹ NMN_OBLIQUE-POSSESSA-PL_FRONT    ;

LEXICON NMN_OLMA !!= @CODE@  олма:олма
!! Back harmony
  NMN_OLMA-SG ;
  NMN_OLMA-PL ;
LEXICON NMN_OLMA-SG
        +Sg+Nom:                    K                              ;
                                    NMN_OBLIQUE-INDEFINITE-SG_BACK ;
                                    NMN_OBLIQUE-POSSESSA-SG_BACK   ;
! +Sg+Nom+PxSg1+So/PC:эм                  K                              ;
! +Sg+Nom+PxSg2+So/PC:ет                  K                              ;
! +Sg+Lat+PxSg3+So/CP:%>еш%{ыӹØ%}ж%{ыӹØ%} K                              ;

LEXICON NMN_OLMA-PL
                      NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
                      NMN_OBLIQUE-POSSESSA-PL_BACK     ;

! +Pl+Nom+PxSg1+So/PNC:эмвлӓ K                                ;
! +Pl+Nom+PxSg1+So/PNC:мвлӓ  K                                ;
! +Pl+Nom+PxSg1+So/NPC:влӓэм K                                ;
! +Pl+Nom+PxSg2+So/PNC:етвлӓ K                                ;
! +Pl+Nom+PxSg2+So/PNC:твлӓ  K                                ;
! +Pl+Nom+PxSg2+So/NCP:влӓэт K                                ;

LEXICON NMN_AEZAE !!= @CODE@  ӓзӓ:ӓзӓ
!! Front harmony
  NMN_AEZAE-SG ;
  NMN_AEZAE-PL ;
LEXICON NMN_AEZAE-SG
        +Sg+Nom:                    K                               ;
                                    NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
                                    NMN_OBLIQUE-POSSESSA-SG_FRONT   ;
! +Sg+Nom+PxSg1+So/PC:эм                  K                               ;
! +Sg+Nom+PxSg2+So/PC:ет                  K                               ;
! +Sg+Lat+PxSg3+So/CP:%>еш%{ыӹØ%}ж%{ыӹØ%} K                               ;

LEXICON NMN_AEZAE-PL
                      NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
                      NMN_OBLIQUE-POSSESSA-PL_FRONT    ;

! +Pl+Nom+PxSg1+So/PNC:эмвлӓ K                                ;
! +Pl+Nom+PxSg1+So/PNC:мвлӓ  K                                ;
! +Pl+Nom+PxSg1+So/NPC:влӓэм K                                ;
! +Pl+Nom+PxSg2+So/PNC:етвлӓ K                                ;
! +Pl+Nom+PxSg2+So/PNC:твлӓ  K                                ;
! +Pl+Nom+PxSg2+So/NCP:влӓэт K                                ;

LEXICON NMN_PECHEN1E !!= @CODE@  печенье:печенье
!! Front harmony
  NMN_PECHEN1E-SG ;
  NMN_PECHEN1E-PL ;
LEXICON NMN_PECHEN1E-SG
 +Sg+Nom: K                               ;
          NMN_OBLIQUE-INDEFINITE-SG_FRONT ;
          NMN_OBLIQUE-POSSESSA-SG_FRONT   ;

LEXICON NMN_PECHEN1E-PL
  NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
  NMN_OBLIQUE-POSSESSA-PL_FRONT    ;

LEXICON N_AEVAE !!= @CODE@  ӓвӓ:ӓвӓ
                           :%{front%}                    NMN_OBLIQUE-INDEFINITE-SG_FRONT  ;
                           :%{front%}             NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
                           :%{front%}%^KIN               NMN_OBLIQUE-POSSESSA-SG_FRONT    ; !!!!
                           :%{front%}%^KIN               NMN_OBLIQUE-POSSESSA-PL_FRONT    ;

                    +Sg+Nom:%{front%}                    K                                ;
              +Sg+Nom+PxSg1+So/PC:%{front%}%>эм                  K                                ;
! !2013-10-18!+Sg+Nom+PxSg1+So/PC:%{front%}%>м                   K                                ; !! Special
              +Pl+Nom+PxSg1+So/PNC:%{front%}%>эмвлӓ               K                                ;
! !2013-10-18!+Pl+Nom+PxSg1+So/PNC:%{front%}%>мвлӓ                K                                ;
! !2013-10-18!+Pl+Nom+PxSg1+So/NPC:влӓэм               K                                ;
              +Sg+Nom+PxSg2+So/PC:%{front%}%>ет                  K                                ;
! !2013-10-18!+Sg+Nom+PxSg2+So/PC:%{front%}%>т                   K                                ; !! Special
              +Pl+Nom+PxSg2+So/PNC:%{front%}%>етвлӓ               K                                ;
! !2013-10-18!+Pl+Nom+PxSg2+So/PNC:твлӓ                K                                ;
! !2013-10-18!+Pl+Nom+PxSg2+So/NCP:влӓэт               K                                ;
! !2013-10-18!+Sg+Lat+PxSg3+So/CP:%>эш%{ыӹØ%}ж%{ыӹØ%} K                                ;

!!

LEXICON PL-ABEACCCOMDATGENILLINELATNOM_MUTUAL
  +Pl+Abe:влӓде    K ;
  +Pl+Acc:%>влӓм   K ;
  +Pl+Com:%>влӓге  K ;
 +Pl+Cmpr:%>влӓлӓ  K ;
  +Pl+Dat:%>влӓлӓн K ;
  +Pl+Gen:%>влӓн   K ;
  +Pl+Ill:%>влӓшкӹ K ;
  +Pl+Ill:%>влӓш   K ;
  +Pl+Ine:%>влӓштӹ K ;
  +Pl+Lat:%>влӓэш  K ;
  +Pl+Nom:%>влӓ    K ;

LEXICON NMN_OBLIQUE-INDEFINITE
  NMN_OBLIQUE-INDEFINITE-SG        ; !!!!!!
  NMN_OBLIQUE-INDEFINITE-PL_MUTUAL ;
LEXICON NMN_OBLIQUE-INDEFINITE-SG
  +Sg+Gen:%>%{ыӹØ%}н    K ; !
  +Sg+Acc:%>%{ыӹØ%}м    K ; !
  +Sg+Com:%>ге          K ; ! (with a village)
  +Sg+Dat:%>л%{аӓ%}н    K ; !
  +Sg+Ine:%>%{ыӹØ%}шт%{ыӹ%} K ; ! (in a village)
  +Sg+Ill:%>%{ыӹØ%}шк%{ыӹ%} K ; ! (to a village)
  +Sg+Ill:%>%{ыӹØ%}ш    K ; ! (to a village)
  +Sg+Lat:%>еш          K ; ! (into a village)
 +Sg+Cmpr:%>л%{аӓ%}     K ; ! (like a village)
  +Sg+Abe:%>де          K ; ! (without a village)

LEXICON NMN_OBLIQUE-INDEFINITE-SG_BACK
  +Sg+Gen:%>ын   K ; !
  +Sg+Acc:%>ым   K ; !
  +Sg+Com:%>ге   K ; ! (with a village)
  +Sg+Dat:%>лан  K ; !
  +Sg+Ine:%>ышты K ; ! (in a village)
  +Sg+Ill:%>ышкы K ; ! (to a village)
  +Sg+Ill:%>ыш   K ; ! (to a village)
  +Sg+Lat:%>еш   K ; ! (into a village)
 +Sg+Cmpr:%>ла   K ; ! (like a village)
  +Sg+Abe:%>де   K ; ! (without a village)

LEXICON NMN_OBLIQUE-INDEFINITE-SG_FRONT
  +Sg+Gen:%>ӹн   K ; !
  +Sg+Acc:%>ӹм   K ; !
  +Sg+Com:%>ге   K ; ! (with a village)
  +Sg+Dat:%>лӓн  K ; !
  +Sg+Ine:%>ӹштӹ K ; ! (in a village)
  +Sg+Ill:%>ӹшкӹ K ; ! (to a village)
  +Sg+Ill:%>ӹш   K ; ! (to a village)
  +Sg+Lat:%>еш   K ; ! (into a village)
 +Sg+Cmpr:%>лӓ   K ; ! (like a village)
  +Sg+Abe:%>де   K ; ! (without a village)

LEXICON NMN_OBLIQUE-INDEFINITE-PL_MUTUAL
!!
  +Pl+Nom:%>влӓ    K ;
  +Pl+Gen:%>влӓн   K ; !
  +Pl+Acc:%>влӓм   K ; !
  +Pl+Com:%>влӓге  K ; ! (with a village)
  +Pl+Dat:%>влӓлӓн K ; !
  +Pl+Ine:%>влӓштӹ K ; ! (in a village)
  +Pl+Ill:%>влӓшкӹ K ; ! (to a village)
  +Pl+Ill:%>влӓш   K ; ! (to a village)
  +Pl+Lat:%>влӓэш  K ; ! (into a village)
 +Pl+Cmpr:%>влӓлӓ  K ; ! (like a village)
  +Pl+Abe:%>влӓде  K ; ! (without a village)



LEXICON N_01
  +Sg+Gen:%>%{ыӹØ%}н            K ; !
  +Sg+Acc:%>%{ыӹØ%}м            K ; !
  +Sg+Com:%>ге                  K ; ! (with a village)
  +Sg+Dat:%>л%{аӓ%}н            K ; !
  +Sg+Ine:%>%{ыӹØ%}шт%{ыӹØ%}    K ; ! (in a village)
  +Sg+Ill:%>%{ыӹØ%}шк%{ыӹØ%}    K ; ! (to a village)
  +Sg+Ill:%>%{ыӹØ%}ш            K ; ! (to a village)
  +Sg+Lat:%>еш                  K ; ! (into a village)
 +Sg+Cmpr:%>л%{аӓ%}             K ; ! (like a village)
  +Sg+Abe:%>де                  K ; ! (without a village)
!!
  +Pl+Nom:влӓ                   K ;
  +Pl+Gen:влӓ%>%{ыӹØ%}н         K ; !
  +Pl+Acc:влӓ%>%{ыӹØ%}м         K ; !
  +Pl+Com:влӓ%>ге               K ; ! (with a village)
  +Pl+Dat:влӓ%>л%{аӓ%}н         K ; !
  +Pl+Ine:влӓ%>%{ыӹØ%}шт%{ыӹØ%} K ; ! (in a village)
  +Pl+Ill:влӓ%>%{ыӹØ%}шк%{ыӹØ%} K ; ! (to a village)
  +Pl+Ill:влӓ%>%{ыӹØ%}ш         K ; ! (to a village)
  +Pl+Lat:влӓ%>Е2ш              K ; ! (into a village)
 +Pl+Cmpr:влӓ%>л%{аӓ%}          K ; ! (like a village)
  +Pl+Abe:влӓ%>де               K ; ! (without a village)

LEXICON NMN_OBLIQUE-POSSESSA-SG_BACK
!! PxSg1+NB+CASE
!! singular possessa
!!

!!
!     кол+Sg+Nom+PxSg1+So/PC: [колем, колэм] # х2 колэмжӹ

!!PxSg1
  SG-ABE-PXSG1_MUTUAL  ;
  SG-ACC-PXSG1_MUTUAL  ;
  SG-COM-PXSG1_MUTUAL  ;
  SG-CMPR-PXSG1_MUTUAL ;
  SG-DAT-PXSG1_MUTUAL  ;
  SG-CMPR-PXSG1_BACK   ;
  SG-DAT-PXSG1_BACK    ;
  SG-GEN-PXSG1_MUTUAL  ;
  SG-ILL-PXSG1_MUTUAL  ;
  SG-INE-PXSG1_MUTUAL  ;
  SG-ILL-PXSG1_BACK    ;
  SG-INE-PXSG1_BACK    ;
  SG-LAT-PXSG1_MUTUAL  ;
  SG-NOM-PXSG1_MUTUAL  ;
!!PxSg2
  SG-ABE-PXSG2_MUTUAL  ;
  SG-ACC-PXSG2_MUTUAL  ;
  SG-COM-PXSG2_MUTUAL  ;
  SG-CMPR-PXSG2_MUTUAL ;
  SG-CMPR-PXSG2_BACK   ;
  SG-DAT-PXSG2_MUTUAL  ;
  SG-GEN-PXSG2_MUTUAL  ;
  SG-ILL-PXSG2_MUTUAL  ;
  SG-INE-PXSG2_MUTUAL  ;
  SG-ILL-PXSG2_BACK    ;
  SG-INE-PXSG2_BACK    ;
  SG-LAT-PXSG2_MUTUAL  ;
  SG-NOM-PXSG2_MUTUAL  ;
!!PxSg3
  SG-ABE-PXSG3_BACK    ;
  SG-ACC-PXSG3_BACK    ;
  SG-COM-PXSG3_BACK    ;
  SG-CMPR-PXSG3_BACK   ;
  SG-DAT-PXSG3_BACK    ;
  SG-GEN-PXSG3_BACK    ;
  SG-ILL-PXSG3_BACK    ;
  SG-INE-PXSG3_BACK    ;
  SG-LAT-PXSG3_BACK    ;
  SG-NOM-PXSG3_BACK    ;
!!PxPl1
  PXPL1-SG-ABE         ;
  PXPL1-SG-ACC         ;
  PXPL1-SG-COM         ;
  PXPL1-SG-CMPR        ;
  PXPL1-SG-DAT         ;
  PXPL1-SG-GEN         ;
  SG-ILL-PXPL1_BACK    ;
  SG-INE-PXPL1_BACK    ;
  PXPL1-SG-LAT         ;
  PXPL1-SG-NOM         ;
!!PxPl2
  PXPL2-SG-ABE         ;
  PXPL2-SG-ACC         ;
  PXPL2-SG-COM         ;
  PXPL2-SG-CMPR        ;
  PXPL2-SG-DAT         ;
  PXPL2-SG-GEN         ;
  SG-ILL-PXPL2_BACK    ;
  SG-INE-PXPL2_BACK    ;
  PXPL2-SG-LAT         ;
  SG-NOM-PXPL2         ;
!!PxPl3
  SG-ABE-PXPL3_BACK    ;
  SG-ACC-PXPL3_BACK    ;
  SG-COM-PXPL3_BACK    ;
  SG-CMPR-PXPL3_BACK   ;
  SG-DAT-PXPL3_BACK    ;
  SG-GEN-PXPL3_BACK    ;
  SG-ILL-PXPL3_BACK    ;
  SG-INE-PXPL3_BACK    ;
  SG-LAT-PXPL3_MUTUAL  ;
  SG-NOM-PXPL3_BACK    ;

LEXICON NMN_OBLIQUE-POSSESSA-SG_FRONT
!! PxSg1+NB+CASE
!! singular possessa
!!

!!
!     кол+N+Sg+Nom+PxSg1+So/PC: [колем, колэм] # х2 колэмжӹ

!!PxSg1
  SG-ABE-PXSG1_MUTUAL  ;
  SG-ACC-PXSG1_MUTUAL  ;
  SG-COM-PXSG1_MUTUAL  ;
  SG-CMPR-PXSG1_MUTUAL ;
  SG-DAT-PXSG1_MUTUAL  ;
  SG-CMPR-PXSG1_FRONT  ;
  SG-DAT-PXSG1_FRONT   ;
  SG-GEN-PXSG1_MUTUAL  ;
  SG-ILL-PXSG1_MUTUAL  ;
  SG-INE-PXSG1_MUTUAL  ;
  SG-ILL-PXSG1_FRONT   ;
  SG-INE-PXSG1_FRONT   ;
  SG-LAT-PXSG1_MUTUAL  ;
  SG-NOM-PXSG1_MUTUAL  ;
!!PxSg2
  SG-ABE-PXSG2_MUTUAL  ;
  SG-ACC-PXSG2_MUTUAL  ;
  SG-COM-PXSG2_MUTUAL  ;
  SG-CMPR-PXSG2_MUTUAL ;
  SG-CMPR-PXSG2_FRONT  ;
  SG-DAT-PXSG2_MUTUAL  ;
  SG-GEN-PXSG2_MUTUAL  ;
  SG-ILL-PXSG2_MUTUAL  ;
  SG-INE-PXSG2_MUTUAL  ;
  SG-ILL-PXSG2_FRONT   ;
  SG-INE-PXSG2_FRONT   ;
  SG-LAT-PXSG2_MUTUAL  ;
  SG-NOM-PXSG2_MUTUAL  ;
!!PxSg3
  SG-ABE-PXSG3_FRONT   ;
  SG-ACC-PXSG3_FRONT   ;
  SG-COM-PXSG3_FRONT   ;
  SG-CMPR-PXSG3_FRONT  ;
  SG-DAT-PXSG3_FRONT   ;
  SG-GEN-PXSG3_FRONT   ;
  SG-ILL-PXSG3_FRONT   ;
  SG-INE-PXSG3_FRONT   ;
  SG-LAT-PXSG3_FRONT   ;
  SG-NOM-PXSG3_FRONT   ;
!!PxPl1
  PXPL1-SG-ABE         ;
  PXPL1-SG-ACC         ;
  PXPL1-SG-COM         ;
  PXPL1-SG-CMPR        ;
  PXPL1-SG-DAT         ;
  PXPL1-SG-GEN         ;
  SG-ILL-PXPL1_FRONT   ;
  SG-INE-PXPL1_FRONT   ;
  PXPL1-SG-LAT         ;
  PXPL1-SG-NOM         ;
!!PxPl2
  PXPL2-SG-ABE         ;
  PXPL2-SG-ACC         ;
  PXPL2-SG-COM         ;
  PXPL2-SG-CMPR        ;
  PXPL2-SG-DAT         ;
  PXPL2-SG-GEN         ;
  SG-ILL-PXPL2_FRONT   ;
  SG-INE-PXPL2_FRONT   ;
  PXPL2-SG-LAT         ;
  SG-NOM-PXPL2         ;
!!PxPl3
  SG-ABE-PXPL3_FRONT   ;
  SG-ACC-PXPL3_FRONT   ;
  SG-COM-PXPL3_FRONT   ;
  SG-CMPR-PXPL3_FRONT  ;
  SG-DAT-PXPL3_FRONT   ;
  SG-GEN-PXPL3_FRONT   ;
  SG-ILL-PXPL3_FRONT   ;
  SG-INE-PXPL3_FRONT   ;
  SG-LAT-PXPL3_MUTUAL  ;
  SG-NOM-PXPL3_FRONT   ;

LEXICON NMN_OBLIQUE-POSSESSA-PL_BACK
! plural possessa

!!PxSg1
  PL-ABE-PXSG1_MUTUAL  ;
  PL-ACC-PXSG1_MUTUAL  ;
  PL-COM-PXSG1_MUTUAL  ;
  PL-CMPR-PXSG1_MUTUAL ;
  PL-DAT-PXSG1_MUTUAL  ;
  PL-GEN-PXSG1_MUTUAL  ;
  PL-ILL-PXSG1_MUTUAL  ;
  PL-INE-PXSG1_MUTUAL  ;
  PL-LAT-PXSG1_MUTUAL  ;
  PL-NOM-PXSG1_MUTUAL  ;

!!PxSg2
  PL-ABE-PXSG2_MUTUAL  ;
  PL-ACC-PXSG2_MUTUAL  ;
  PL-COM-PXSG2_MUTUAL  ;
  PL-CMPR-PXSG2_MUTUAL ;
  PL-DAT-PXSG2_MUTUAL  ;
  PL-GEN-PXSG2_MUTUAL  ;
  PL-ILL-PXSG2_MUTUAL  ;
  PL-INE-PXSG2_MUTUAL  ;
  PL-LAT-PXSG2_MUTUAL  ;
  PL-NOM-PXSG2_MUTUAL  ;

!!PxSg3
  PL-ABE-PXSG3_MUTUAL  ;
  PL-ACC-PXSG3_MUTUAL  ;
  PL-COM-PXSG3_MUTUAL  ;
  PL-CMPR-PXSG3_MUTUAL ;
  PL-DAT-PXSG3_MUTUAL  ;
  PL-GEN-PXSG3_MUTUAL  ;
  PL-ILL-PXSG3_MUTUAL  ;
  PL-INE-PXSG3_MUTUAL  ;
  PL-LAT-PXSG3_MUTUAL  ;
  PL-NOM-PXSG3_MUTUAL  ;
  PL-ABE-PXSG3_BACK    ;
  PL-ACC-PXSG3_BACK    ;
  PL-COM-PXSG3_BACK    ;
  PL-CMPR-PXSG3_BACK   ;
  PL-DAT-PXSG3_BACK    ;
  PL-GEN-PXSG3_BACK    ;
  PL-ILL-PXSG3_BACK    ;
  PL-INE-PXSG3_BACK    ;
  PL-LAT-PXSG3_BACK    ;
  PL-NOM-PXSG3_BACK    ;

!!PxPl1
  PL-ABE-PXPL1_MUTUAL  ;
  PL-ACC-PXPL1_MUTUAL  ;
  PL-COM-PXPL1_MUTUAL  ;
  PL-CMPR-PXPL1_MUTUAL ;
  PL-DAT-PXPL1_MUTUAL  ;
  PL-GEN-PXPL1_MUTUAL  ;
  PL-ILL-PXPL1_MUTUAL  ;
  PL-INE-PXPL1_MUTUAL  ;
  PL-LAT-PXPL1_MUTUAL  ;
  PL-NOM-PXPL1_MUTUAL  ;

!!PxPl2
  PL-ABE-PXPL2_MUTUAL  ;
  PL-ACC-PXPL2_MUTUAL  ;
  PL-COM-PXPL2_MUTUAL  ;
  PL-CMPR-PXPL2_MUTUAL ;
  PL-DAT-PXPL2_MUTUAL  ;
  PL-GEN-PXPL2_MUTUAL  ;
  PL-ILL-PXPL2_MUTUAL  ;
  PL-INE-PXPL2_MUTUAL  ;
  PL-LAT-PXPL2_MUTUAL  ;
  PL-NOM-PXPL2_MUTUAL  ;

!!PxPl3
  PL-ABE-PXPL3_MUTUAL  ;
  PL-ACC-PXPL3_MUTUAL  ;
  PL-COM-PXPL3_MUTUAL  ;
  PL-CMPR-PXPL3_MUTUAL ;
  PL-DAT-PXPL3_MUTUAL  ;
  PL-GEN-PXPL3_MUTUAL  ;
  PL-ILL-PXPL3_MUTUAL  ;
  PL-INE-PXPL3_MUTUAL  ;
  PL-LAT-PXPL3_MUTUAL  ;
  PL-NOM-PXPL3_MUTUAL  ;

LEXICON NMN_OBLIQUE-POSSESSA-PL_FRONT
! plural possessa

!!PxSg1
  PL-ABE-PXSG1_MUTUAL  ;
  PL-ACC-PXSG1_MUTUAL  ;
  PL-COM-PXSG1_MUTUAL  ;
  PL-CMPR-PXSG1_MUTUAL ;
  PL-DAT-PXSG1_MUTUAL  ;
  PL-GEN-PXSG1_MUTUAL  ;
  PL-ILL-PXSG1_MUTUAL  ;
  PL-INE-PXSG1_MUTUAL  ;
  PL-LAT-PXSG1_MUTUAL  ;
  PL-NOM-PXSG1_MUTUAL  ;

!!PxSg2
  PL-ABE-PXSG2_MUTUAL  ;
  PL-ACC-PXSG2_MUTUAL  ;
  PL-COM-PXSG2_MUTUAL  ;
  PL-CMPR-PXSG2_MUTUAL ;
  PL-DAT-PXSG2_MUTUAL  ;
  PL-GEN-PXSG2_MUTUAL  ;
  PL-ILL-PXSG2_MUTUAL  ;
  PL-INE-PXSG2_MUTUAL  ;
  PL-LAT-PXSG2_MUTUAL  ;
  PL-NOM-PXSG2_MUTUAL  ;

!!PxSg3
  PL-ABE-PXSG3_MUTUAL  ;
  PL-ACC-PXSG3_MUTUAL  ;
  PL-COM-PXSG3_MUTUAL  ;
  PL-CMPR-PXSG3_MUTUAL ;
  PL-DAT-PXSG3_MUTUAL  ;
  PL-GEN-PXSG3_MUTUAL  ;
  PL-ILL-PXSG3_MUTUAL  ;
  PL-INE-PXSG3_MUTUAL  ;
  PL-LAT-PXSG3_MUTUAL  ;
  PL-NOM-PXSG3_MUTUAL  ;

  PL-ABE-PXSG3_FRONT   ;
  PL-ACC-PXSG3_FRONT   ;
  PL-COM-PXSG3_FRONT   ;
  PL-CMPR-PXSG3_FRONT  ;
  PL-DAT-PXSG3_FRONT   ;
  PL-GEN-PXSG3_FRONT   ;
  PL-ILL-PXSG3_FRONT   ;
  PL-INE-PXSG3_FRONT   ;
  PL-LAT-PXSG3_FRONT   ;
  PL-NOM-PXSG3_FRONT   ;

!!PxPl1
  PL-ABE-PXPL1_MUTUAL  ;
  PL-ACC-PXPL1_MUTUAL  ;
  PL-COM-PXPL1_MUTUAL  ;
  PL-CMPR-PXPL1_MUTUAL ;
  PL-DAT-PXPL1_MUTUAL  ;
  PL-GEN-PXPL1_MUTUAL  ;
  PL-ILL-PXPL1_MUTUAL  ;
  PL-INE-PXPL1_MUTUAL  ;
  PL-LAT-PXPL1_MUTUAL  ;
  PL-NOM-PXPL1_MUTUAL  ;

!!PxPl2
  PL-ABE-PXPL2_MUTUAL  ;
  PL-ACC-PXPL2_MUTUAL  ;
  PL-COM-PXPL2_MUTUAL  ;
  PL-CMPR-PXPL2_MUTUAL ;
  PL-DAT-PXPL2_MUTUAL  ;
  PL-GEN-PXPL2_MUTUAL  ;
  PL-ILL-PXPL2_MUTUAL  ;
  PL-INE-PXPL2_MUTUAL  ;
  PL-LAT-PXPL2_MUTUAL  ;
  PL-NOM-PXPL2_MUTUAL  ;

!!PxPl3
  PL-ABE-PXPL3_MUTUAL  ;
  PL-ACC-PXPL3_MUTUAL  ;
  PL-COM-PXPL3_MUTUAL  ;
  PL-CMPR-PXPL3_MUTUAL ;
  PL-DAT-PXPL3_MUTUAL  ;
  PL-GEN-PXPL3_MUTUAL  ;
  PL-ILL-PXPL3_MUTUAL  ;
  PL-INE-PXPL3_MUTUAL  ;
  PL-LAT-PXPL3_MUTUAL  ;
  PL-NOM-PXPL3_MUTUAL  ;


!!
LEXICON SG-ABE-PXSG1_MUTUAL
 +Sg+Abe+PxSg1+So/CP:%>деэм K ; !NB_CX_PX
 +Sg+Abe+PxSg1+So/PC:%>емде K ; !NB_PX_CX

LEXICON SG-ACC-PXSG1_MUTUAL
 +Sg+Acc+PxSg1+So/PC:%>емӹм K ;

LEXICON SG-COM-PXSG1_MUTUAL
 +Sg+Com+PxSg1+So/PC:%>емге K ;

LEXICON SG-CMPR-PXSG1_MUTUAL
 +Sg+Cmpr+PxSg1+So/PC:%>емлӓ K ; !NB_PX_CX
LEXICON SG-CMPR-PXSG1_BACK
 +Sg+Cmpr+PxSg1+So/CP:%>ла%>эм K ;
LEXICON SG-CMPR-PXSG1_FRONT
 +Sg+Cmpr+PxSg1+So/CP:%>лӓ%>эм K ;

LEXICON SG-DAT-PXSG1_MUTUAL
 +Sg+Dat+PxSg1+So/PC:%>емлӓн K ;
LEXICON SG-DAT-PXSG1_BACK
 +Sg+Dat+PxSg1+So/CP:%>ланем K ;
LEXICON SG-DAT-PXSG1_FRONT
 +Sg+Dat+PxSg1+So/CP:%>лӓнем K ;

LEXICON SG-GEN-PXSG1_MUTUAL
 +Sg+Gen+PxSg1+So/PC:%>емӹн K ;

LEXICON SG-ILL-PXSG1_MUTUAL
 +Sg+Ill+PxSg1+So/PC:%>емӹшкӹ K ;
LEXICON SG-ILL-PXSG1_BACK
 +Sg+Ill+PxSg1+So/CP:%>ышкем K ;
LEXICON SG-ILL-PXSG1_FRONT
 +Sg+Ill+PxSg1+So/CP:%>ӹшкем K ;

LEXICON SG-INE-PXSG1_MUTUAL
 +Sg+Ine+PxSg1+So/PC:%>емӹштӹ K ;
LEXICON SG-INE-PXSG1_BACK
 +Sg+Ine+PxSg1+So/CP:%>ыштем K ;
LEXICON SG-INE-PXSG1_FRONT
 +Sg+Ine+PxSg1+So/CP:%>ӹштем K ;

LEXICON SG-LAT-PXSG1_MUTUAL
 +Sg+Lat+PxSg1+So/CP:%>ешем K ; !NB_CX_PX
 +Sg+Lat+PxSg1+So/PC:%>емӹш K ; !NB_PX_CX

LEXICON SG-NOM-PXSG1_MUTUAL
 +Sg+Nom+PxSg1+So/CP:%>ем K ;

!+!
LEXICON PL-ABE-PXSG1_MUTUAL
 +Pl+Abe+PxSg1+So/NPC:%>влӓэмде K ;
 +Pl+Abe+PxSg1+So/PNC:%>емвлӓде K ;

LEXICON PL-ACC-PXSG1_MUTUAL
 +Pl+Acc+PxSg1+So/PNC:%>емвлӓм  K ;
 +Pl+Acc+PxSg1+So/NPC:%>влӓэмӹм K ;

LEXICON PL-COM-PXSG1_MUTUAL
 +Pl+Com+PxSg1+So/PNC:%>емвлӓге K ;
 +Pl+Com+PxSg1+So/NPC:%>влӓэмге K ;

LEXICON PL-CMPR-PXSG1_MUTUAL
 +Pl+Cmpr+PxSg1+So/NPC:%>влӓэмлӓ K ;
 +Pl+Cmpr+PxSg1+So/NCP:%>влӓлӓэм K ;
 +Pl+Cmpr+PxSg1+So/PNC:%>емвлӓлӓ K ;

LEXICON PL-DAT-PXSG1_MUTUAL
  +Pl+Dat+PxSg1+So/NPC:%>влӓэмлӓн K ;
  +Pl+Dat+PxSg1+So/PNC:%>емвлӓлӓн K ;
  +Pl+Dat+PxSg1+So/NCP:%>влӓлӓнем K ;
! +Pl+Dat+PxSg1+So/PNC:%>емвлӓлӓ  K ; !!= * __@CODE@__ Is this really supposed to be here

LEXICON PL-GEN-PXSG1_MUTUAL
 +Pl+Gen+PxSg1+So/PNC:%>емвлӓн  K ;
 +Pl+Gen+PxSg1+So/NPC:%>влӓэмӹн K ;

LEXICON PL-ILL-PXSG1_MUTUAL
 +Pl+Ill+PxSg1+So/NCP:%>влӓшкем  K ;
 +Pl+Ill+PxSg1+So/PNC:%>емвлӓшкӹ K ;
 +Pl+Ill+PxSg1+So/PNC:%>емвлӓш   K ;
 +Pl+Ill+PxSg1+So/NCP:%>влӓшкем  K ;

LEXICON PL-INE-PXSG1_MUTUAL
 +Pl+Ine+PxSg1+So/NCP:%>влӓштем  K ;
 +Pl+Ine+PxSg1+So/PNC:%>емвлӓштӹ K ;

LEXICON PL-LAT-PXSG1_MUTUAL
 +Pl+Lat+PxSg1+So/NCP:%>влӓэшем K ;
 +Pl+Lat+PxSg1+So/PNC:%>емвлӓэш K ;
 +Pl+Lat+PxSg1+So/PNC:%>емвлӓш  K ;

LEXICON PL-NOM-PXSG1_MUTUAL
 +Pl+Nom+PxSg1+So/NCP:%>влӓэм K ;
 +Pl+Nom+PxSg1+So/PNC:%>емвлӓ K ;

!!
LEXICON SG-ABE-PXSG2_MUTUAL
 +Sg+Abe+PxSg2+So/CP:%>деэт K ;
 +Sg+Abe+PxSg2+So/PC:%>етде K ;

LEXICON SG-ACC-PXSG2_MUTUAL
 +Sg+Acc+PxSg2+So/PC:%>етӹм K ;

LEXICON SG-COM-PXSG2_MUTUAL
 +Sg+Com+PxSg2+So/PC:%>етге K ;

LEXICON SG-CMPR-PXSG2_MUTUAL
 +Sg+Cmpr+PxSg2+So/PC:%>етлӓ K ;
LEXICON SG-CMPR-PXSG2_BACK
 +Sg+Cmpr+PxSg2+So/CP:%>лаэт K ;
LEXICON SG-CMPR-PXSG2_FRONT
 +Sg+Cmpr+PxSg2+So/CP:%>лӓэт K ;

LEXICON SG-DAT-PXSG2_MUTUAL
 +Sg+Dat+PxSg2+So/PC:%>етлӓн K ;

LEXICON SG-GEN-PXSG2_MUTUAL
 +Sg+Gen+PxSg2+So/PC:%>етӹн K ;

LEXICON SG-ILL-PXSG2_MUTUAL
 +Sg+Ill+PxSg2+So/PC:%>етӹшкӹ K ;
 +Sg+Ill+PxSg2+So/PC:%>етӹш   K ;
LEXICON SG-ILL-PXSG2_BACK
 +Sg+Ill+PxSg2+So/CP:%>ышкет K ;
LEXICON SG-ILL-PXSG2_FRONT
 +Sg+Ill+PxSg2+So/CP:%>ӹшкет K ;

LEXICON SG-INE-PXSG2_MUTUAL
 +Sg+Ine+PxSg2+So/PC:%>етӹштӹ K ;
LEXICON SG-INE-PXSG2_BACK
 +Sg+Ine+PxSg2+So/CP:%>ыштет K ;
LEXICON SG-INE-PXSG2_FRONT
 +Sg+Ine+PxSg2+So/CP:%>ӹштет K ;

LEXICON SG-LAT-PXSG2_MUTUAL
 +Sg+Lat+PxSg2+So/CP:%>ешет K ;
 +Sg+Lat+PxSg2+So/PC:%>етӹш K ;

LEXICON SG-NOM-PXSG2_MUTUAL
 +Sg+Nom+PxSg2+So/CP:%>ет K ;

LEXICON PL-ABE-PXSG2_MUTUAL
 +Pl+Abe+PxSg2+So/NPC:%>влӓэтде K ;
 +Pl+Abe+PxSg2+So/PNC:%>етвлӓде K ;

LEXICON PL-ACC-PXSG2_MUTUAL
 +Pl+Acc+PxSg2+So/PNC:%>етвлӓм  K ;
 +Pl+Acc+PxSg2+So/NPC:%>влӓэтӹм K ;

LEXICON PL-COM-PXSG2_MUTUAL
 +Pl+Com+PxSg2+So/PNC:%>етвлӓге K ;
 +Pl+Com+PxSg2+So/NPC:%>влӓэтге K ;

LEXICON PL-CMPR-PXSG2_MUTUAL
 +Pl+Cmpr+PxSg2+So/NPC:%>влӓэтлӓ K ;
 +Pl+Cmpr+PxSg2+So/NCP:%>влӓлӓэт K ;
 +Pl+Cmpr+PxSg2+So/PNC:%>етвлӓлӓ K ;

LEXICON PL-DAT-PXSG2_MUTUAL
  +Pl+Dat+PxSg2+So/NPC:%>влӓэтлӓн K ;
  +Pl+Dat+PxSg2+So/PNC:%>етвлӓлӓн K ;
  +Pl+Dat+PxSg2+So/NCP:%>влӓлӓнет K ;
! +Pl+Dat+PxSg2+So/PNC:%>етвлӓлӓ  K ; !!= * __@CODE@__ Is this really supposed to be here

LEXICON PL-GEN-PXSG2_MUTUAL
 +Pl+Gen+PxSg2+So/PNC:%>етвлӓн  K ;
 +Pl+Gen+PxSg2+So/NPC:%>влӓэтӹн K ;

LEXICON PL-ILL-PXSG2_MUTUAL
 +Pl+Ill+PxSg2+So/NCP:%>влӓшкет  K ;
 +Pl+Ill+PxSg2+So/PNC:%>етвлӓшкӹ K ;
 +Pl+Ill+PxSg2+So/PNC:%>етвлӓш   K ;

LEXICON PL-INE-PXSG2_MUTUAL
 +Pl+Ine+PxSg2+So/NCP:%>влӓштет  K ;
 +Pl+Ine+PxSg2+So/PNC:%>етвлӓштӹ K ;

LEXICON PL-LAT-PXSG2_MUTUAL
  +Pl+Lat+PxSg2+So/NCP:%>влӓэшет K ;
! +Pl+Lat+PxSg2+So/PNC:%>етвлӓэш K ;
  +Pl+Lat+PxSg2+So/PNC:%>етвлӓэш K ;

LEXICON PL-NOM-PXSG2_MUTUAL
  +Pl+Nom+PxSg2+So/PNC:%>етвлӓ   K ;
  +Pl+Nom+PxSg2+So/NCP:%>влӓэт   K ;

!! PXSG3
!LEXICON PXSG3-SG-ABE
! +Sg+Abe+PxSg3+So/CP:%>де%>ж%{ыӹ%} K ;
! +Sg+Abe+PxSg3+So/PC:%>ж%{ыӹ%}%>де K ;

LEXICON SG-ABE-PXSG3_BACK
 +Sg+Abe+PxSg3+So/CP:%>де%>жӹ K ;
 +Sg+Abe+PxSg3+So/PC:%>жы%>де K ;

LEXICON SG-ABE-PXSG3_FRONT
  +Sg+Abe+PxSg3+So/CP:%>де%>жӹ K ;
  +Sg+Abe+PxSg3+So/PC:%>жӹ%>де K ;

!LEXICON PXSG3-SG-ACC
! +Sg+Acc+PxSg3+So/PC:%>ж%{ыӹ%}%>м K ;

LEXICON SG-ACC-PXSG3_BACK
 +Sg+Acc+PxSg3+So/PC:%>жы%>м K ;

LEXICON SG-ACC-PXSG3_FRONT
  +Sg+Acc+PxSg3+So/PC:%>жӹ%>м   K ;

!LEXICON PXSG3-SG-COM
! +Sg+Com+PxSg3+So/PC:%>ж%{ыӹ%}%>ге K ;

LEXICON SG-COM-PXSG3_BACK
 +Sg+Com+PxSg3+So/PC:%>жы%>ге K ;

LEXICON SG-COM-PXSG3_FRONT
   +Sg+Com+PxSg3+So/PC:%>жӹ%>ге       K ;

!LEXICON PXSG3-SG-CMPR
! +Sg+Cmpr+PxSg3+So/CP:%>л%{аӓ%}%>ж%{ыӹ%} K ;
! +Sg+Cmpr+PxSg3+So/PC:%>ж%{ыӹ%}%>л%{аӓ%} K ;

LEXICON SG-CMPR-PXSG3_BACK
 +Sg+Cmpr+PxSg3+So/CP:%>ла%>жы K ;
 +Sg+Cmpr+PxSg3+So/PC:%>жы%>ла K ;

LEXICON SG-CMPR-PXSG3_FRONT
  +Sg+Cmpr+PxSg3+So/CP:%>лӓ%>жӹ        K ;
  +Sg+Cmpr+PxSg3+So/PC:%>жӹ%>лӓ        K ;

!LEXICON PXSG3-SG-DAT
!  +Sg+Dat+PxSg3+So/PC:%>ж%{ыӹ%}%>л%{аӓ%}н K ;
LEXICON SG-DAT-PXSG3_BACK
 +Sg+Dat+PxSg3+So/PC:%>жы%>лан K ;
LEXICON SG-DAT-PXSG3_FRONT
 +Sg+Dat+PxSg3+So/PC:%>жӹ%>лӓн K ;

LEXICON SG-GEN-PXSG3_BACK
 +Sg+Gen+PxSg3+So/PC:%>жы%>н K ;
LEXICON SG-GEN-PXSG3_FRONT
 +Sg+Gen+PxSg3+So/PC:%>жӹ%>н K ;

LEXICON SG-ILL-PXSG3_BACK
 +Sg+Ill+PxSg3+So/CP:%>ышкы%>жы K ;
 +Sg+Ill+PxSg3+So/PC:%>жы%>шкы  K ;
 +Sg+Ill+PxSg3+So/PC:%>жы%>ш    K ;
LEXICON SG-ILL-PXSG3_FRONT
 +Sg+Ill+PxSg3+So/CP:%>ӹшкӹ%>жӹ K ;
 +Sg+Ill+PxSg3+So/PC:%>жӹ%>шкӹ  K ;
 +Sg+Ill+PxSg3+So/PC:%>жӹ%>ш    K ;

LEXICON SG-INE-PXSG3_BACK
        +Sg+Ine+PxSg3+So/CP:%>ышты%>жы K ;
 +Use/NG+Sg+Ine+PxSg3+So/CP:%>ышты%>ш  K ;
        +Sg+Ine+PxSg3+So/PC:%>жы%>шты  K ;
LEXICON SG-INE-PXSG3_FRONT
        +Sg+Ine+PxSg3+So/CP:%>ӹштӹ%>жӹ K ;
 +Use/NG+Sg+Ine+PxSg3+So/CP:%>ӹштӹ%>ш  K ;
        +Sg+Ine+PxSg3+So/PC:%>жӹ%>штӹ  K ;

LEXICON SG-LAT-PXSG3_BACK
 +Sg+Lat+PxSg3+So/CP:%>ешӹжӹ K ;
 +Sg+Lat+PxSg3+So/PC:%>жыш   K ;
LEXICON SG-LAT-PXSG3_FRONT
 +Sg+Lat+PxSg3+So/CP:%>ешӹжӹ K ;
 +Sg+Lat+PxSg3+So/PC:%>жӹш   K ;

LEXICON SG-NOM-PXSG3_BACK
 +Sg+Nom+PxSg3+So/CP:%>жы K ;
LEXICON SG-NOM-PXSG3_FRONT
 +Sg+Nom+PxSg3+So/CP:%>жӹ K ;

LEXICON PL-ABE-PXSG3_MUTUAL
 +Pl+Abe+PxSg3+So/NPC:%>влӓжӹде K ;
LEXICON PL-ABE-PXSG3_BACK
 +Pl+Abe+PxSg3+So/PNC:%>жы%>влӓде K ;
LEXICON PL-ABE-PXSG3_FRONT
 +Pl+Abe+PxSg3+So/PNC:%>жӹ%>влӓде K ;

LEXICON PL-ACC-PXSG3_MUTUAL
 +Pl+Acc+PxSg3+So/NPC:%>влӓжӹм K ;
LEXICON PL-ACC-PXSG3_BACK
 +Pl+Acc+PxSg3+So/PNC:%>жы%>влӓм K ;
LEXICON PL-ACC-PXSG3_FRONT
 +Pl+Acc+PxSg3+So/PNC:%>жӹ%>влӓм K ;

LEXICON PL-COM-PXSG3_MUTUAL
 +Pl+Com+PxSg3+So/NPC:%>влӓжӹге K ;
LEXICON PL-COM-PXSG3_BACK
 +Pl+Com+PxSg3+So/PNC:%>жы%>влӓге K ;
LEXICON PL-COM-PXSG3_FRONT
 +Pl+Com+PxSg3+So/PNC:%>жӹ%>влӓге K ;

LEXICON PL-CMPR-PXSG3_MUTUAL
 +Pl+Cmpr+PxSg3+So/NPC:%>влӓжӹлӓ K ;
 +Pl+Cmpr+PxSg3+So/NCP:%>влӓлӓжӹ K ;
LEXICON PL-CMPR-PXSG3_BACK
 +Pl+Cmpr+PxSg3+So/PNC:%>жы%>влӓлӓ K ;
LEXICON PL-CMPR-PXSG3_FRONT
 +Pl+Cmpr+PxSg3+So/PNC:%>жӹ%>влӓлӓ K ;

LEXICON PL-DAT-PXSG3_MUTUAL
 +Pl+Dat+PxSg3+So/NPC:%>влӓжӹлӓн K ;
 +Pl+Dat+PxSg3+So/NCP:%>влӓлӓнжӹ K ;
LEXICON PL-DAT-PXSG3_BACK
 +Pl+Dat+PxSg3+So/PNC:%>жы%>влӓлӓн K ;
LEXICON PL-DAT-PXSG3_FRONT
 +Pl+Dat+PxSg3+So/PNC:%>жӹ%>влӓлӓн K ;

LEXICON PL-GEN-PXSG3_MUTUAL
 +Pl+Gen+PxSg3+So/NPC:%>влӓжӹн K ;
LEXICON PL-GEN-PXSG3_BACK
 +Pl+Gen+PxSg3+So/PNC:%>жы%>влӓн K ;
LEXICON PL-GEN-PXSG3_FRONT
 +Pl+Gen+PxSg3+So/PNC:%>жӹ%>влӓн K ;

LEXICON PL-ILL-PXSG3_MUTUAL
 +Pl+Ill+PxSg3+So/NCP:%>влӓшкӹжӹ K ;
 +Pl+Ill+PxSg3+So/NPC:%>влӓжӹш   K ;
 +Pl+Ill+PxSg3+So/NPC:%>влӓжӹшкӹ K ;
LEXICON PL-ILL-PXSG3_BACK
 +Pl+Ill+PxSg3+So/PNC:%>жы%>влӓшкӹ K ;
 +Pl+Ill+PxSg3+So/PNC:%>жы%>влӓш   K ;
LEXICON PL-ILL-PXSG3_FRONT
 +Pl+Ill+PxSg3+So/PNC:%>жӹ%>влӓшкӹ K ;
 +Pl+Ill+PxSg3+So/PNC:%>жӹ%>влӓш   K ;

LEXICON PL-INE-PXSG3_MUTUAL
 +Pl+Ine+PxSg3+So/NPC:%>влӓжӹштӹ K ;
 +Pl+Ine+PxSg3+So/NCP:%>влӓштӹжӹ K ;
LEXICON PL-INE-PXSG3_BACK
 +Pl+Ine+PxSg3+So/PNC:%>жы%>влӓштӹ K ;
LEXICON PL-INE-PXSG3_FRONT
 +Pl+Ine+PxSg3+So/PNC:%>жӹ%>влӓштӹ K ;

LEXICON PL-LAT-PXSG3_MUTUAL
 +Pl+Lat+PxSg3+So/NCP:%>влӓэшӹжӹ K ;
LEXICON PL-LAT-PXSG3_BACK
 +Pl+Lat+PxSg3+So/PNC:%>жы%>влӓэш K ;
LEXICON PL-LAT-PXSG3_FRONT
 +Pl+Lat+PxSg3+So/PNC:%>жӹ%>влӓэш K ;

LEXICON PL-NOM-PXSG3_MUTUAL
 +Pl+Nom+PxSg3+So/NCP:%>влӓжӹ K ;
LEXICON PL-NOM-PXSG3_BACK
 +Pl+Nom+PxSg3+So/PNC:%>жы%>влӓ K ;
LEXICON PL-NOM-PXSG3_FRONT
 +Pl+Nom+PxSg3+So/PNC:%>жӹ%>влӓ K ;

!+!



LEXICON PXPL1-SG-ABE
 +Sg+Abe+PxPl1+So/PC:%>н%{аӓ%}%>де K ;

LEXICON PXPL1-SG-ACC
 +Sg+Acc+PxPl1+So/PC:%>н%{аӓ%}%>%{ыӹØ%}м K ;

LEXICON PXPL1-SG-COM
 +Sg+Com+PxPl1+So/PC:%>н%{аӓ%}%>ге K ;

LEXICON PXPL1-SG-CMPR
 +Sg+Cmpr+PxPl1+So/CP:%>л%{аӓ%}%>н%{аӓ%} K ;
 +Sg+Cmpr+PxPl1+So/PC:%>н%{аӓ%}%>л%{аӓ%} K ;

LEXICON PXPL1-SG-DAT
 +Sg+Dat+PxPl1+So/PC:%>н%{аӓ%}%>л%{аӓ%}н K ;
 +Sg+Dat+PxPl1+So/CP:%>л%{аӓ%}н%>н%{аӓ%} K ;

LEXICON PXPL1-SG-GEN
  +Sg+Gen+PxPl1+So/PC:%>н%{аӓ%}%>%{ыӹØ%}н         K ;

!LEXICON PXPL1-SG-ILL
! +Sg+Ill+PxPl1+So/CP:%>%{ыӹØ%}шк%{ыӹØ%}%>н%{аӓ%} K ;
LEXICON SG-ILL-PXPL1_BACK
 +Sg+Ill+PxPl1+So/CP:%>ышкы%>на K ;
LEXICON SG-ILL-PXPL1_FRONT
  +Sg+Ill+PxPl1+So/CP:%>ӹшкӹ%>нӓ                  K ;
!LEXICON PXPL1-SG-INE
! +Sg+Ine+PxPl1+So/CP:%>%{ыӹØ%}шт%{ыӹØ%}%>н%{аӓ%} K ;
LEXICON SG-INE-PXPL1_BACK
 +Sg+Ine+PxPl1+So/CP:%>ышты%>на K ;
LEXICON SG-INE-PXPL1_FRONT
 +Sg+Ine+PxPl1+So/CP:%>ӹштӹ%>нӓ K ;

LEXICON PXPL1-SG-LAT
 +Sg+Lat+PxPl1+So/CP:%>еш%>н%{аӓ%} K ;

LEXICON PXPL1-SG-NOM
 +Sg+Nom+PxPl1+So/CP:%>н%{аӓ%} K ;

!+!
LEXICON PL-ABE-PXPL1_MUTUAL
 +Pl+Abe+PxPl1+So/NPC:%>влӓнӓде K ;
LEXICON PL-ACC-PXPL1_MUTUAL
 +Pl+Acc+PxPl1+So/NPC:%>влӓнӓм K ;
LEXICON PL-COM-PXPL1_MUTUAL
 +Pl+Com+PxPl1+So/NPC:%>влӓнӓге K ;
LEXICON PL-CMPR-PXPL1_MUTUAL
 +Pl+Cmpr+PxPl1+So/NPC:%>влӓнӓлӓ K ;

LEXICON PL-DAT-PXPL1_MUTUAL
 +Pl+Dat+PxPl1+So/NPC:%>влӓнӓлӓн K ;
 +Pl+Dat+PxPl1+So/NCP:%>влӓлӓннӓ K ;

LEXICON PL-GEN-PXPL1_MUTUAL
 +Pl+Gen+PxPl1+So/NPC:%>влӓ%>нӓ%>ӹн K ;
LEXICON PL-ILL-PXPL1_MUTUAL
 +Pl+Ill+PxPl1+So/NCP:%>влӓшкӹнӓ K ;
LEXICON PL-INE-PXPL1_MUTUAL
 +Pl+Ine+PxPl1+So/NCP:%>влӓштӹнӓ K ;
LEXICON PL-LAT-PXPL1_MUTUAL
 +Pl+Lat+PxPl1+So/NCP:%>влӓэшнӓ K ;
LEXICON PL-NOM-PXPL1_MUTUAL
 +Pl+Nom+PxPl1+So/NCP:%>влӓ%>нӓ K ;

!!PxPl2
LEXICON PXPL2-SG-ABE
 +Sg+Abe+PxPl2+So/PC:%>д%{аӓ%}%>де K ;
LEXICON PXPL2-SG-ACC
 +Sg+Acc+PxPl2+So/PC:%>д%{аӓ%}%>%{ыӹØ%}м K ;
LEXICON PXPL2-SG-COM
 +Sg+Com+PxPl2+So/PC:%>д%{аӓ%}%>ге K ;
LEXICON PXPL2-SG-CMPR
 +Sg+Cmpr+PxPl2+So/CP:%>л%{аӓ%}%>д%{аӓ%} K ;
 +Sg+Cmpr+PxPl2+So/PC:%>д%{аӓ%}%>л%{аӓ%} K ;
LEXICON PXPL2-SG-DAT
 +Sg+Dat+PxPl2+So/PC:%>д%{аӓ%}%>л%{аӓ%}н K ;
 +Sg+Dat+PxPl2+So/CP:%>л%{аӓ%}н%>д%{аӓ%} K ;
LEXICON PXPL2-SG-GEN
  +Sg+Gen+PxPl2+So/PC:%>д%{аӓ%}%>%{ыӹØ%}н         K ;
!LEXICON PXPL2-SG-ILL
! +Sg+Ill+PxPl2+So/CP:%>%{ыӹØ%}шк%{ыӹØ%}%>д%{аӓ%} K ;
LEXICON SG-ILL-PXPL2_BACK
 +Sg+Ill+PxPl2+So/CP:%>ышкы%>да K ;
LEXICON SG-ILL-PXPL2_FRONT
  +Sg+Ill+PxPl2+So/CP:%>ӹшкӹ%>дӓ                  K ;
!LEXICON PXPL2-SG-INE
! +Sg+Ine+PxPl2+So/CP:%>%{ыӹØ%}шт%{ыӹØ%}%>д%{аӓ%} K ;
LEXICON SG-INE-PXPL2_BACK
 +Sg+Ine+PxPl2+So/CP:%>ышты%>да K ;
LEXICON SG-INE-PXPL2_FRONT
 +Sg+Ine+PxPl2+So/CP:%>ӹштӹ%>дӓ K ;
LEXICON PXPL2-SG-LAT
 +Sg+Lat+PxPl2+So/CP:%>еш%>дӓ K ;
LEXICON SG-NOM-PXPL2
 +Sg+Nom+PxPl2+So/CP:%>д%{аӓ%} K ;

!+!
LEXICON PL-ABE-PXPL2_MUTUAL
 +Pl+Abe+PxPl2+So/NPC:%>влӓ%>дӓ%>де K ;
LEXICON PL-ACC-PXPL2_MUTUAL
 +Pl+Acc+PxPl2+So/NPC:%>влӓ%>дӓ%>ӹм K ;
LEXICON PL-COM-PXPL2_MUTUAL
 +Pl+Com+PxPl2+So/NPC:%>влӓ%>дӓ%>ге K ;
LEXICON PL-CMPR-PXPL2_MUTUAL
 +Pl+Cmpr+PxPl2+So/NPC:%>влӓ%>дӓ%>лӓ K ;
LEXICON PL-DAT-PXPL2_MUTUAL
  +Pl+Dat+PxPl2+So/NPC:%>влӓ%>дӓлӓн   K ;
  +Pl+Dat+PxPl2+So/NCP:%>влӓ%>лӓндӓ   K ;
! +Pl+Dat+PxPl2+So/NPC:%>влӓ%>дӓ%>лӓн K ;
! +Pl+Dat+PxPl2+So/NCP:%>влӓ%>лӓн%>дӓ K ;
LEXICON PL-GEN-PXPL2_MUTUAL
 +Pl+Gen+PxPl2+So/NPC:%>влӓ%>дӓ%>ӹн K ;
LEXICON PL-ILL-PXPL2_MUTUAL
  +Pl+Ill+PxPl2+So/NCP:%>влӓ%>шкӹдӓ    K ;
! +Pl+Ill+PxPl2+So/NCP:%>влӓ%>ӹшкӹ%>дӓ K ;
LEXICON PL-INE-PXPL2_MUTUAL
  +Pl+Ine+PxPl2+So/NCP:%>влӓ%>штӹдӓ    K ;
! +Pl+Ine+PxPl2+So/NCP:%>влӓ%>ӹштӹ%>дӓ K ;
LEXICON PL-LAT-PXPL2_MUTUAL
  +Pl+Lat+PxPl2+So/NCP:%>влӓэшдӓ      K ;
! +Pl+Lat+PxPl2+So/NCP:%>влӓ%>Е2ш%>дӓ K ;
LEXICON PL-NOM-PXPL2_MUTUAL
  +Pl+Nom+PxPl2+So/NCP:%>влӓ%>дӓ              K ;

!!PxPl3
!LEXICON PXPL3-SG-ABE
! +Sg+Abe+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%}%>де K ;
LEXICON SG-ABE-PXPL3_BACK
 +Sg+Abe+PxPl3+So/PC:%>ышты%>де K ;
LEXICON SG-ABE-PXPL3_FRONT
  +Sg+Abe+PxPl3+So/PC:%>ӹштӹ%>де                   K ;
!LEXICON PXPL3-SG-ACC
! +Sg+Acc+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}м K ;
LEXICON SG-ACC-PXPL3_BACK
 +Sg+Acc+PxPl3+So/PC:%>ышты%>ым K ;
LEXICON SG-ACC-PXPL3_FRONT
  +Sg+Acc+PxPl3+So/PC:%>ӹштӹ%>ӹм             K ;
!LEXICON PXPL3-SG-COM
! +Sg+Com+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%}%>ге K ;
LEXICON SG-COM-PXPL3_BACK
 +Sg+Com+PxPl3+So/PC:%>ышты%>ге K ;
LEXICON SG-COM-PXPL3_FRONT
   +Sg+Com+PxPl3+So/PC:%>ӹштӹ%>ге                  K ;
!LEXICON PXPL3-SG-CMPR
! +Sg+Cmpr+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%}%>л%{аӓ%} K ;
LEXICON SG-CMPR-PXPL3_BACK
 +Sg+Cmpr+PxPl3+So/PC:%>ышты%>ла K ;
 +Sg+Cmpr+PxPl3+So/CP:%>ла%>ышты K ;
LEXICON SG-CMPR-PXPL3_FRONT
  +Sg+Cmpr+PxPl3+So/PC:%>ӹштӹ%>лӓ                   K ;
  +Sg+Cmpr+PxPl3+So/CP:%>лӓ%>ӹштӹ                   K ;
!LEXICON PXPL3-SG-DAT
!  +Sg+Dat+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%}%>л%{аӓ%}н K ;
!  +Sg+Dat+PxPl3+So/CP:%>л%{аӓ%}н%>%{ыӹØ%}шт%{ыӹØ%} K ;
LEXICON SG-DAT-PXPL3_BACK
 +Sg+Dat+PxPl3+So/PC:%>ышты%>лан K ;
 +Sg+Dat+PxPl3+So/CP:%>лан%>ышты K ;
LEXICON SG-DAT-PXPL3_FRONT
  +Sg+Dat+PxPl3+So/PC:%>ӹштӹ%>лӓн                  K ;
  +Sg+Dat+PxPl3+So/CP:%>лӓн%>ӹштӹ                  K ;
!LEXICON PXPL3-SG-GEN
! +Sg+Gen+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}н K ;
LEXICON SG-GEN-PXPL3_BACK
 +Sg+Gen+PxPl3+So/PC:%>ышты%>ын K ;
LEXICON SG-GEN-PXPL3_FRONT
  +Sg+Gen+PxPl3+So/PC:%>ӹштӹ%>ӹн                           K ;
!LEXICON PXPL3-SG-ILL
! +Sg+Ill+PxPl3+So/CP:%>%{ыӹØ%}шк%{ыӹØ%}%>%{ыӹØ%}шт%{ыӹØ%} K ;
LEXICON SG-ILL-PXPL3_BACK
 +Sg+Ill+PxPl3+So/CP:%>ышкы%>ышты K ;
LEXICON SG-ILL-PXPL3_FRONT
  +Sg+Ill+PxPl3+So/CP:%>ӹшкӹ%>ӹштӹ                         K ;
!LEXICON PXPL3-SG-INE
! +Sg+Ine+PxPl3+So/CP:%>%{ыӹØ%}шт%{ыӹØ%}%>%{ыӹØ%}шт%{ыӹØ%} K ;
LEXICON SG-INE-PXPL3_BACK
 +Sg+Ine+PxPl3+So/CP:%>ышты%>ышты K ;
LEXICON SG-INE-PXPL3_FRONT
  +Sg+Ine+PxPl3+So/CP:%>ӹштӹ%>ӹштӹ K ;
!LEXICON PXPL3-SG-LAT
! +Sg+Lat+PxPl3+So/CP:%>еш%>ӹштӹ   K ;
LEXICON SG-LAT-PXPL3_MUTUAL
  +Sg+Lat+PxPl3+So/CP:%>еш%>ӹштӹ         K ;
!LEXICON PXPL3-SG-NOM
! +Sg+Nom+PxPl3+So/PC:%>%{ыӹØ%}шт%{ыӹØ%} K ;
LEXICON SG-NOM-PXPL3_BACK
 +Sg+Nom+PxPl3+So/CP:%>ышты K ;
LEXICON SG-NOM-PXPL3_FRONT
 +Sg+Nom+PxPl3+So/CP:%>ӹштӹ K ;
!+!
LEXICON PL-ABE-PXPL3_MUTUAL
 +Pl+Abe+PxPl3+So/NPC:%>влӓ%>ӹштӹ%>де K ;
LEXICON PL-ACC-PXPL3_MUTUAL
 +Pl+Acc+PxPl3+So/NPC:%>влӓ%>ӹштӹ%>ӹм K ;
LEXICON PL-COM-PXPL3_MUTUAL
 +Pl+Com+PxPl3+So/NPC:%>влӓ%>ӹштӹ%>ге K ;
LEXICON PL-CMPR-PXPL3_MUTUAL
 +Pl+Cmpr+PxPl3+So/NPC:%>влӓ%>ӹштӹ%>лӓ K ;
 +Pl+Cmpr+PxPl3+So/NCP:%>влӓ%>лӓ%>ӹштӹ K ;
LEXICON PL-DAT-PXPL3_MUTUAL
 +Pl+Dat+PxPl3+So/NPC:%>влӓ%>ӹштӹ%>лӓн K ;
 +Pl+Dat+PxPl3+So/NCP:%>влӓ%>лӓн%>ӹштӹ K ;
LEXICON PL-GEN-PXPL3_MUTUAL
 +Pl+Gen+PxPl3+So/NPC:%>влӓ%>ӹштӹ%>ӹн K ;
LEXICON PL-ILL-PXPL3_MUTUAL
 +Pl+Ill+PxPl3+So/NCP:%>влӓ%>ӹшкӹ%>ӹштӹ K ;
LEXICON PL-INE-PXPL3_MUTUAL
 +Pl+Ine+PxPl3+So/NCP:%>влӓ%>ӹштӹ%>ӹштӹ K ;
LEXICON PL-LAT-PXPL3_MUTUAL
 +Pl+Lat+PxPl3+So/NCP:%>влӓ%>Е2ш%>ӹштӹ K ;
LEXICON PL-NOM-PXPL3_MUTUAL
 +Pl+Nom+PxPl3+So/NCP:%>влӓ%>ӹштӹ K ;



! vim: set ft=xfst-lexc:

